;; -----------------------------------------------------------------------------
;; Demo Server-Side-Events Service
;; -----------------------------------------------------------------------------
;;

(load-module :tomcat ['tomcat :as 'tc])
(load-module :ring)
(load-module :server-side-events ['server-side-events :as 'sse])

(import :java.io.IOException)


; ensure the Tomcat libs are on the classpath
(tc/check-required-libs)


;; -----------------------------------------------------------------------------
;; Event Producer
;; -----------------------------------------------------------------------------

(defn event-producer [queue]
  (let [id-counter (atom 1000)]
    (println "Event producer running.")
    (thread #(while true
               (let [id (str (swap! id-counter inc))]
                 (sleep 2000)
                 (println "Adding event " id)
                 (put! queue { :id    id 
                               :event "demo" 
                               :data  ["Counter ~{id}"] }))))))



;; -----------------------------------------------------------------------------
;; Ring handler (publishes the events to the client)
;; -----------------------------------------------------------------------------

(defn events-demo [queue request]
  (let [async_request   (:async-request request)   ;; HttpServletRequest
        async_response  (:async-response request)  ;; HttpServletResponse
        os              (. async_response :getOutputStream)
        pr              (io/wrap-os-with-print-writer os :utf-8)]

    (. async_response :setContentType       "text/event-stream")
    (. async_response :setCharacterEncoding "UTF-8")

    ;; start the event producer
    (event-producer queue)

    (try
      (loop []
        (let [event       (take! queue)
              sse-event   (sse/render event)]
          (println "Publishing event: " (:id event))
          (print pr sse-event)
          (flush pr)
          (recur)))
      (catch :Exception e 
        (println "Client closed connection")))
            
    (println "Stopped serving events")

    ;; return nil to signal the :ring module to send any further to the client
    nil))



;; -----------------------------------------------------------------------------
;; Middleware configuration
;; -----------------------------------------------------------------------------

(def sse-queue (queue))

(def tomcat-opts { :await?    false   ;; do not block - return after start
                   :base-dir  "."
                   :port      8080 })

(def routes [[:get "/events"  (partial events-demo sse-queue)]])


(defn events-servlet []
  (ring/create-servlet (-> (ring/match-routes routes)     ; >--+
                                                          ;    |
                           (ring-mw/mw-dump-response)     ; ^  |
                           (ring-mw/mw-dump-request)      ; |  |
                           (ring-mw/mw-request-counter)   ; |  |
                           (ring-mw/mw-add-session 3600)  ; |  |
                           (ring-mw/mw-print-uri)         ; |  |
                           (ring-mw/mw-debug :on))))      ; +--+

;; start Tomcat
(let [server (tc/start [ [ (events-servlet)
                           { :name          "events-servlet"  
                             :mapping       "/*"
                             :async-support true } ] ]
                       tomcat-opts)]
  (defn stop [] (tc/shutdown server)))





;; GET (demo)
(comment
  (do
    (load-module :http-client-j8 ['http-client-j8 :as 'hc])

    (let [response  (hc/send :get 
                             "http://localhost:8080/events" 
                             :headers { "Accept"         "text/event-stream" 
                                        "Cache-Control"  "no-cache"
                                        "Connection"     "keep-alive"}
                             :conn-timeout 0
                             :read-timeout 0
                             :debug true)]
      (println "Status:" (:http-status response))
      
      (println "\nConsuming events...\n")
      (let [is (:data-stream response)
            rd (io/wrap-is-with-buffered-reader is :utf-8)]
        ;; read the resonse line by line
        (loop [line (read-line rd) event-count 1]
          (if (some? line)
            (do
              (println line)  
              (if (<= event-count 30)
                (recur (read-line rd) (inc event-count))
                (do
                  (println "Stopped after receiving 10 events")
                  (. rd :close))))
            (println "Last event received"))))))
)

