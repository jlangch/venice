;; -----------------------------------------------------------------------------
;; Demo REST Service
;; -----------------------------------------------------------------------------
;;

(load-module :tomcat ['tomcat :as 'tc])
(load-module :ring)


; ensure the Tomcat libs are on the classpath
(tc/check-required-libs)


;; -----------------------------------------------------------------------------
;; ID generator
;; -----------------------------------------------------------------------------

(def id-cnt (atom 999))

(defn gen-id [] (str (swap! id-cnt inc)))


;; -----------------------------------------------------------------------------
;; Employee database
;; -----------------------------------------------------------------------------

(def employees (atom [ { :id (gen-id)  :name "susan"  :role "secretary" }
                       { :id (gen-id)  :name "john"   :role "assistant" }
                       { :id (gen-id)  :name "mary"   :role "team-lead" } ]))


(defn find-employee-by-id [id]
  (first (filter #(= id (:id %)) @employees)))

(defn delete-employee-by-id [id]
  (swap! employees #(filter (fn [e] (not (= id (:id e)))) %)))

(defn add-employee [employee]
  (let [id (coalesce (:id employee) (gen-id))
        e  (assoc (select-keys employee [:name :role]) :id id)]
    (delete-employee-by-id id)
    (swap! employees #(conj % e))
    e))


;; -----------------------------------------------------------------------------
;; Ring handler
;; -----------------------------------------------------------------------------

(defn get-all-employees [request]
  { :status 200
    :headers { "Content-Type" "application/json; charset=utf-8" }
    :body (json/write-str  @employees)})


(defn get-employee [request]
  (let [id       (-> request :parameters :id)
        employee (find-employee-by-id id)]
    (if employee
      { :status 200
        :headers { "Content-Type" "application/json; charset=utf-8" }
        :body (json/write-str employee) }
      { :status 404
        :headers { "Content-Type" "text/plain; charset=utf-8" }
        :body "Employee with the id ~{id} not found!" })))


(defn create-employee [request]
  (let [json?    (ring/json-request? request)
        body     (if json? (json/slurp (:body request) :key-fn keyword) nil)
        employee (if body {:name (:name body), :role (:role body)} nil)]
    (cond
      (not json?) { :status 400
                    :headers { "Content-Type" "text/plain; charset=utf-8" }
                    :body "Not a json request!" }
      :else       { :status 200
                    :headers { "Content-Type" "application/json; charset=utf-8" }
                    :body (json/write-str (add-employee employee)) } )))


(defn update-employee [request]
  (let [id        (-> request :parameters :id)
        employee  (find-employee-by-id id)
        json?     (ring/json-request? request)
        body      (if json? (json/slurp (:body request) :key-fn keyword) nil)
        employee* (if body {:id id, :name (:name body), :role (:role body)} nil)]
    (cond
      (not json?)     { :status 400
                        :headers { "Content-Type" "text/plain; charset=utf-8" }
                        :body "Not a json request!" }
      (nil? employee) { :status 404
                        :headers { "Content-Type" "text/plain; charset=utf-8" }
                        :body "Employee with the id ~{id} not found!" }
      :else           { :status 200
                        :headers { "Content-Type" "application/json; charset=utf-8" }
                        :body (json/write-str (add-employee employee*)) } )))


(defn delete-employee [request]
  (let [id       (-> request :parameters :id)
        employee (find-employee-by-id id)
        _        (when employee (delete-employee-by-id id))]
    (if employee
      { :status 200
        :headers { "Content-Type" "text/plain; charset=utf-8" }
        :body "Employee with the id ~{id} deleted!" }
      { :status 404
        :headers { "Content-Type" "text/plain; charset=utf-8" }
        :body "Employee with the id ~{id} not found!" })))


;; -----------------------------------------------------------------------------
;; Middleware configuration
;; -----------------------------------------------------------------------------

(def tomcat-opts {:await? false, :base-dir ".", :port 8080})

(def routes [[:get    "/employees"      get-all-employees]
             [:get    "/employees/:id"  get-employee]
             [:post   "/employees"      create-employee]
             [:put    "/employees/:id"  update-employee]
             [:delete "/employees/:id"  delete-employee]])

(defn rest-servlet []
  (ring/create-servlet (-> (ring/match-routes routes)  ; >--+
                                                       ;    |
                           (ring/mw-dump-response)     ; ^  |
                           (ring/mw-dump-request)      ; |  |
                           (ring/mw-request-counter)   ; |  |
                           (ring/mw-add-session 3600)  ; |  |
                           (ring/mw-print-uri)         ; |  |
                           (ring/mw-debug :on))))      ; +--+

;; start Tomcat
(let [server (tc/start (rest-servlet) tomcat-opts)]
  (defn stop [] (tc/shutdown server)))


;; -----------------------------------------------------------------------------
;; Curl commands for testing
;;
;; curl -i -X GET http://localhost:8080/employees
;; curl -i -X GET http://localhost:8080/employees/1000
;; curl -i -X DELETE http://localhost:8080/employees/1000
;; curl -i -X POST -H 'Content-Type: application/json' -d '{"name": "hanna", "role": "secretary"}' http://localhost:8080/employees
;; curl -i -X PUT -H 'Content-Type: application/json' -d '{"id": "1001", "name": "john", "role": "clerk"}' http://localhost:8080/employees/1001
;; -----------------------------------------------------------------------------

;; -----------------------------------------------------------------------------
;; Venice HTTP Client examples
;; -----------------------------------------------------------------------------

;; GET (get)
(comment
  (do
    (load-module :http-client-j8 ['http-client-j8 :as 'hc])

    (let [res      (hc/send :get 
                            "http://localhost:8080/employees" 
                            :headers { "Accept" "application/json, text/plain" }
                            :debug true)
          status   (:http-status res)
          mimetype (:content-type-mimetype res)
          charset  (:content-type-charset res)]
      (println "Status:" status)
      (if (and (= 200 status) (= "application/json" mimetype))
        (println (hc/slurp-json (:data-stream res) charset))
        (println (hc/slurp-string (:data-stream res) charset)))))
)

;; POST (create)
(comment
  (do
    (load-module :http-client-j8 ['http-client-j8 :as 'hc])
    
    (let [res      (hc/send :post 
                            "http://localhost:8080/employees" 
                            :headers {"Accept"       "application/json, text/plain"
                                      "Content-Type" "application/json"}
                            :body (json/write-str { "name" "hanna", 
                                                    "role" "secretary" })
                            :debug true)
          status   (:http-status res)
          mimetype (:content-type-mimetype res)
          charset  (:content-type-charset res)]
      (println "Status:" status)
      (if (and (= 200 status) (= "application/json" mimetype))
        (println (hc/slurp-json (:data-stream res) charset))
        (println (hc/slurp-string (:data-stream res) charset)))))
)

;; PUT (update)
(comment 
  (do
    (load-module :http-client-j8 ['http-client-j8 :as 'hc])
    
    (let [res      (hc/send :put 
                            "http://localhost:8080/employees/1001" 
                            :headers {"Accept"       "application/json, text/plain"
                                      "Content-Type" "application/json"}
                            :body (json/write-str { "id"   "1001", 
                                                    "name" "john", 
                                                    "role" "clerk" })
                            :debug true)
          status   (:http-status res)
          mimetype (:content-type-mimetype res)
          charset  (:content-type-charset res)]
      (println "Status:" status)
      (if (and (= 200 status) (= "application/json" mimetype))
        (println (hc/slurp-json (:data-stream res) charset))
        (println (hc/slurp-string (:data-stream res) charset)))))
)

;; DELETE (delete)
(comment 
  (do
    (load-module :http-client-j8 ['http-client-j8 :as 'hc])
    
    (let [res      (hc/send :delete 
                            "http://localhost:8080/employees/1000" 
                            :headers { "Accept" "text/plain" }
                            :debug true)
          status   (:http-status res)
          mimetype (:content-type-mimetype res)
          charset  (:content-type-charset res)]
      (println "Status:" status)
      (if (= "text/plain" mimetype)
        (println (hc/slurp-string (:data-stream res) charset)))))
)
;; -----------------------------------------------------------------------------

(println "Tomcat started on port ~(:port tomcat-opts).")
(println "Open a browser:      (sh/open \"http://localhost:8080\")")
(println "Stop it by calling:  (stop)")
