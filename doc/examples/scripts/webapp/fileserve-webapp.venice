;; -----------------------------------------------------------------------------
;; File serving application
;; -----------------------------------------------------------------------------
;;

(load-module :tomcat ['tomcat :as 'tc])
(load-module :ring)


; ensure the Tomcat libs are on the classpath
(tc/check-required-libs)

(def tomcat-opts {:await? false, :base-dir ".", :port 8080})

(def files-base-dir (io/file-absolute (io/file "." "resources")))


(defn not-found-handler [request]
  { :status 404
    :headers { "Content-Type" "text/plain; charset=utf-8" }
    :body "Not Found!" })


(defn file-handler [request]
  (if-not (str/starts-with? (:uri request) "/static/")
    (not-found-handler request)
    (let [uri  (:uri request)
          file (io/file files-base-dir (str/strip-start uri "/static"))]
      (if (and (io/exists-file? file)
               (io/file-can-read? file)
               (io/file-within-dir? files-base-dir file))
        { :status 200
          :headers { "Content-Type" (io/mime-type (io/file-name file)) }
          :body file }
        (not-found-handler request)))))


;; A route is defined by a HTTP verb, a URI filter and a handle
;; function.
;; If multiple routes match the route with the longest URI filter
;; will be chosen
(def routes [[:get "/**"         not-found-handler]
             [:get "/static/**"  file-handler]])


(defn my-servlet []
  (ring/create-servlet (-> (ring/match-routes routes)  ; >--+
                                                       ;    |
                           ;(ring/mw-dump-response)    ; ^  |
                           ;(ring/mw-dump-request)     ; |  |
                           (ring/mw-request-counter)   ; |  |
                           (ring/mw-add-session 3600)  ; |  |
                           (ring/mw-print-uri)         ; |  |
                           (ring/mw-debug :on))))      ; +--+

(when-not (io/exists-dir? files-base-dir)
  (throw (ex :VncException
             "The file base dir ~{files-base-dir} does not exits!")))

;; start Tomcat
(let [server (tc/start (my-servlet) tomcat-opts)]
  (defn stop [] (tc/shutdown server)))


(println "Tomcat started on port ~(:port opts).")
(println "Open a browser:      (sh/open \"http://localhost:8080\")")
(println "Stop it by calling:  (stop)")
