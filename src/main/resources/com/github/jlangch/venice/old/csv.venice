;;;;   __    __         _
;;;;   \ \  / /__ _ __ (_) ___ ___
;;;;    \ \/ / _ \ '_ \| |/ __/ _ \
;;;;     \  /  __/ | | | | (_|  __/
;;;;      \/ \___|_| |_|_|\___\___|
;;;;
;;;;
;;;; Copyright 2017-2023 Venice
;;;;
;;;; Licensed under the Apache License, Version 2.0 (the "License");
;;;; you may not use this file except in compliance with the License.
;;;; You may obtain a copy of the License at
;;;;
;;;;     http://www.apache.org/licenses/LICENSE-2.0
;;;;
;;;; Unless required by applicable law or agreed to in writing, software
;;;; distributed under the License is distributed on an "AS IS" BASIS,
;;;; WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
;;;; See the License for the specific language governing permissions and
;;;; limitations under the License.

;;;; CSV reader / writer


(ns csv2)

(import :java.io.Writer)
(import :java.io.StringWriter)


(defn
  ^{ :arglists '("(csv2/read source & options)")
     :doc """
          Reads CSV-data from a source. The source may be a a string, a bytebuf,
          a file, a Java InputStream, or a Java Reader

          Options:
            :encoding enc - used when reading from a binary data source
                            e.g :encoding :utf-8, defaults to :utf-8
            :separator val - e.g. ",", defaults to a comma
            :quote val - e.g. "'", defaults to a double quote
          """
     :examples '(
          """
          (do
            (load-module :csv)
            (csv2/read "1,\\"ab\\",false"))
          """,
          """
          (do
            (load-module :csv)
            (csv2/read "1:::'ab':false" :separator ":" :quote "'"))
          """ ) }

  csv2/read [source & options]

  (let [data (if (string? source)
                source
                (io/slurp source (conj options :binary false)))
        opts (apply hash-map options)
        separator (str (coalesce (:separator opts) csv2/comma))
        quote (str (coalesce (:quote opts) csv2/double-quote))
        two-quotes (str quote quote)
        pattern (regex/pattern (csv2/make-regex separator quote))]

    (map #(csv2/parse-line % pattern separator quote two-quotes)
         (str/split-lines data))))


(defn
  ^{ :arglists '("(csv2/write writer records & options)")
     :doc """
          Writes data to a writer in CSV format. The writer is a
          Java java.io.Writer

          Options:
            :separator val - e.g. ",", defaults to a comma
            :quote val - e.g. "'", defaults to a double quote
            :newline val (:lf (default) or :cr+lf)
          """
     :examples '(
          """
          (do
            (load-module :csv2)

            (let [file (io/file "test.csv")
                  fs (. :java.io.FileOutputStream :new file)]
              (try-with [writer (. :java.io.OutputStreamWriter :new fs "utf-8")]
                (csv2/write writer [[1 "AC" false] [2 "WS" true]]))))
          """ ) }

  csv2/write [writer records & options]

  (let [opts (apply hash-map options)
        separator (str (coalesce (:separator opts) csv2/comma))
        quote (str (coalesce (:quote opts) csv2/double-quote))
        newline (:newline opts :lf)]
    (csv2/write* writer
                records
                separator
                quote
                (csv2/newlines newline))))


(defn
  ^{ :arglists '("(csv2/write-str records & options)")
     :doc """
          Writes data to a string in CSV format.

          Options:
            :separator val - e.g. ",", defaults to a comma
            :quote val - e.g. "'", defaults to a double quote
            :newline val (:lf (default) or :cr+lf)
          """
     :examples '(
          """
          (do
            (load-module :csv2)
            (csv2/write-str [[1 "AC" false] [2 "WS" true]]))
          """,
          """
          (do
            (load-module :csv2)
            (csv2/write-str [[1 "AC" false] [2 "WS, '-1'" true]]
                           :quote "'"
                           :separator ","
                           :newline :cr+lf))
          """ ) }

  csv2/write-str [records & options]

  (let [opts (apply hash-map options)
        separator (str (coalesce (:separator opts) csv2/comma))
        quote (str (coalesce (:quote opts) csv2/double-quote))
        newline (:newline opts :lf)]
    (try-with [writer (. :StringWriter :new)]
      (csv2/write* writer
                  records
                  separator
                  quote
                  (csv2/newlines newline))
      (. writer :flush)
      (str writer))))


(def csv2/double-quote (char 34))
(def csv2/comma (char 44))
(def csv2/newlines {:lf "\n" :cr+lf "\r\n"})


(defn- csv2/make-regex [separator quote]
  (str/format "[%c]|[^%c%c]+|[%c](?:[^%c]|[%c][%c])*[%c]"
              (char separator)
              (char separator)
              (char quote)
              (char quote)
              (char quote)
              (char quote)
              (char quote)
              (char quote)))

(defn- csv2/split [s pattern]
  (regex/find-all (regex/matcher pattern s)))

(defn- csv2/unquote [s quote two-quotes]
  (if (and (str/starts-with? s quote) (str/ends-with? s quote))
      (-> (str/strip-end (str/strip-start s quote) quote)
          (str/replace-all two-quotes quote))
      s))

(defn- csv2/parse-line [s pattern separator quote two-quotes]
  (let [elements (csv2/split s pattern)]
    (loop [items elements values []]
       (let [head (first items)]
         (cond
           (nil? head) values
           (== head separator) (recur (rest items) (conj values nil))
           :else (recur (rest (rest items))
                        (conj values (csv2/unquote head quote two-quotes))))))))

(defn- csv2/escape [s quote]
  (->> (str/split s (str "[" quote "]"))
       (str/join (str quote quote))))

(defn- csv2/quote? [s quote separator]
  (or (str/contains? s " ")
      (str/contains? s quote)
      (str/contains? s separator)))

(defn- csv2/quote [s quote separator]
  (if (csv2/quote? s quote separator)
      (str quote (csv2/escape s quote) quote)
      s))

(defn- csv2/write-record [writer record separator quote]
  (when (some? record)
     (. writer :write
        (str/join separator
                  (map #(csv2/quote (str %) quote separator) record)))))

(defn- csv2/write* [writer records separator quote newline]
  (when-let [record (first records)]
    (write-record writer record separator quote)
    (loop [records (rest records)]
      (when-not (empty? records)
        (. writer :write newline)
        (write-record writer (first records) separator quote)
        (recur (rest records))))
    (. writer :flush)))
