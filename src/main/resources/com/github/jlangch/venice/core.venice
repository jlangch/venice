;;;;   __    __         _
;;;;   \ \  / /__ _ __ (_) ___ ___
;;;;    \ \/ / _ \ '_ \| |/ __/ _ \
;;;;     \  /  __/ | | | | (_|  __/
;;;;      \/ \___|_| |_|_|\___\___|
;;;;
;;;;
;;;; Copyright 2017-2021 Venice
;;;;
;;;; Licensed under the Apache License, Version 2.0 (the "License");
;;;; you may not use this file except in compliance with the License.
;;;; You may obtain a copy of the License at
;;;;
;;;;     http://www.apache.org/licenses/LICENSE-2.0
;;;;
;;;; Unless required by applicable law or agreed to in writing, software
;;;; distributed under the License is distributed on an "AS IS" BASIS,
;;;; WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
;;;; See the License for the specific language governing permissions and
;;;; limitations under the License.

;;;; Venice core macros & functions


(ns core)


(defmacro
  ^{ :arglists '("(comment & body)")
     :doc "Ignores body, yields nil"
     :examples '(
          """
          (comment
            (println 1)
            (println 5))
          """ ) }

   comment [& body] nil)


(defmacro
  ^{ :arglists '(
          "(assert expr)" "(assert expr message)")
     :doc """
          Evaluates expr and throws an `:com.github.jlangch.venice.AssertionException`
          exception if it does not evaluate to logical true.
          """
     :examples '(
          "(assert (= 3 (+ 1 2)))"
          "(assert (= 4 (+ 1 2)))" ) }

  assert

  ([expr]
    `(if-not ~expr
       (throw (ex :com.github.jlangch.venice.AssertionException
                  (str "Assert failed: " (pr-str '~expr))))
       true))

  ([expr message]
    `(if-not ~expr
       (throw (ex :com.github.jlangch.venice.AssertionException
                  (str "Assert failed (" ~message "): " (pr-str '~expr))))
       true)))


(defmacro
  ^{ :arglists '(
          "(defn name [args*] condition-map? expr*)"
          "(defn name ([args*] condition-map? expr*)+)")
     :doc """
          Same as `(def name (fn name [args*] condition-map? expr*))` or
          `(def name (fn name ([args*] condition-map? expr*)+))`
          """
     :examples '(
          "(defn sum [x y] (+ x y))"
          "(defn sum [x y] { :pre [(> x 0)] } (+ x y))"
          """
          (defn sum
            ([] 0)
            ([x] x)
            ([x y] (+ x y)))
          """)
     :see-also '("defn-" "fn" "def") }

   defn [name & forms]

   (if (vector? (first forms))
      `(def ~name (fn ~name ~(first forms) ~@(rest forms)))
      `(def ~name (fn ~name ~@forms))))


(defmacro
  ^{ :arglists '(
          "(defn- name [args*] condition-map? expr*)"
          "(defn- name ([args*] condition-map? expr*)+)")
     :doc "Same as `defn`, yielding non-public `def`"
     :examples '("(defn- sum [x y] (+ x y))")
     :see-also '("defn" "fn" "def") }

   defn- [name & forms]

   (list* `defn (with-meta name (assoc (meta name) :private true)) forms))


(defmacro
  ^{ :arglists '("(and x)" "(and x & next)")
     :doc "Ands the predicate forms"
     :examples '("(and true true)", "(and true false)", "(and)")
     :see-also '("or" "not") }

  and

  ([] true)
  ([x] x)
  ([x & next] `(let [cond# ~x]
                  (if cond# (and ~@next) cond#))))


(defmacro
  ^{ :arglists '("(or x)" "(or x & next)")
     :doc "Ors the predicate forms"
     :examples '("(or true false)", "(or false false)", "(or)")
     :see-also '("and" "not") }

  or

  ([] false)
  ([x] x)
  ([x & next] `(let [cond# ~x]
                  (if cond# cond# (or ~@next)))))


(defn
  ^{ :arglists '("(not x)")
     :doc "Returns true if x is logical false, false otherwise."
     :examples '("(not true)"
                 "(not (== 1 2))")
     :see-also '("and" "or") }

  not

  [x] (if x false true))


(defmacro
  ^{ :arglists '("(cond & clauses)")
     :doc """
          Takes a set of test/expr pairs. It evaluates each test one at a time.
          If a test returns logical true, cond evaluates and returns the value
          of the corresponding expr and doesn't evaluate any of the other tests
          or exprs. `(cond)` returns `nil`.
          """
     :examples '(
          """
          (let [n 5]
            (cond
              (< n 0) "negative"
              (> n 0) "positive"
              :else "zero"))
          """ )
     :see-also '("condp" "case") }

  cond [& clauses]

  (when (not-empty? clauses)
    (list 'if (first clauses)
          (if (rest clauses)
              (second clauses)
              (throw (ex :VncException "cond requires an even number of forms")))
          (cons 'cond (rest (rest clauses))))))


(defmacro
  ^{ :arglists '("(condp pred expr & clauses)")
     :doc """
          Takes a binary predicate, an expression, and a set of clauses.

          Each clause can take the form of either:¶
          &ensp; `test-expr result-expr`¶
          &ensp; `test-expr :>> result-fn`¶
          Note `:>>` is an ordinary keyword.

          For each clause, (pred test-expr expr) is evaluated. If it returns
          logical true, the clause is a match. If a binary clause matches, the
          result-expr is returned, if a ternary clause matches, its result-fn,
          which must be a unary function, is called with the result of the
          predicate as its argument, the result of that call being the return
          value of condp. A single default expression can follow the clauses,
          and its value will be returned if no clause matches. If no default
          expression is provided and no clause matches, a VncException
          is thrown.
          """
     :examples '(
          """
          (condp some [1 2 3 4]
             #{0 6 7} :>> inc
             #{4 5 9} :>> dec
             #{1 2 3} :>> #(* % 10))
          """,
          """
          (condp some [-10 -20 0 10]
            pos?  1
            neg? -1
            (constantly true)  0)
          """ )
     :see-also '("cond" "case" )  }

  condp [pred expr & clauses]

  (let [gpred (gensym "pred__")
        gexpr (gensym "expr__")
        emit (fn emit [pred expr args]
               (let [[[a b c :as clause] more]
                       (split-at (if (== :>> (second args)) 3 2) args)
                       n (count clause)]
                 (cond
                  (== 0 n) `(throw (ex :VncException
                                       (str "No matching condp clause: " ~expr)))
                  (== 1 n) a
                  (== 2 n) `(if (~pred ~a ~expr)
                              ~b
                              ~(emit pred expr more))
                  :else (let [p (gensym "pred__")]
                           `(if-let [~p (~pred ~a ~expr)]
                              (~c ~p)
                              ~(emit pred expr more))))))]
    `(let [~gpred ~pred
           ~gexpr ~expr]
       ~(emit gpred gexpr clauses))))


(defmacro
  ^{ :arglists '("(case expr & clauses)")
     :doc """
          Takes an expression and a set of clauses. Each clause takes the form
          of test-constant result-expr
          """
     :examples '(
          """
          (case (+ 1 9)
            10  :ten
            20  :twenty
            30  :thirty
            :dont-know)
          """ )
     :see-also '("cond" "condp") }

     ;; rewrites to (let [e (+ 1 9)]
     ;;                (cond
     ;;                  (== e 10) :ten
     ;;                  (== e 20) :twenty
     ;;                  (== e 30) :thirty
     ;;                  :else :dont-know))

  case [expr & clauses]

  (let [expr-sym (gensym "expr_")
        default_ (if (odd? (count clauses)) (last clauses) nil)
        pairs_ (partition 2 (if (odd? (count clauses)) (butlast clauses) clauses))]
    `(let [~expr-sym ~expr]
       (cond
         ~@(mapcat (fn [[test-const_ result-expr_]]
                       `((== ~expr-sym ~test-const_) ~result-expr_))
                      pairs_)
         :else ~default_))))


(defmacro
  ^{ :arglists '(
          "(if-not test then else)",
          "(if-not test then)")
     :doc """
          Evaluates test. If logical false, evaluates and returns then
          expression, otherwise else expression, if supplied, else nil.
          """
     :examples '(
          "(if-not (== 1 2) 100 0)",
          "(if-not false 100)",
          "(if-not true 100)")
     :see-also '("if" "if-let" "when" "when-not" "when-let")  }

  if-not

  ([test then] `(if-not ~test ~then nil))
  ([test then else] `(if (not ~test) ~then ~else)))


(defmacro
  ^{ :arglists '("(when test & body)")
     :doc "Evaluates test. If logical true, evaluates body in an implicit do."
     :examples '("(when (== 1 1) true)")
     :see-also '("when-not" "when-let" "if" "if-not" "if-let") }

  when [test & body]

  `(if ~test (do ~@body)))


(defmacro
  ^{ :arglists '("(when-not test & body)")
     :doc "Evaluates test. If logical false, evaluates body in an implicit do."
     :examples '("(when-not (== 1 2) true)")
     :see-also '("when" "when-let" "if" "if-not" "if-let") }

  when-not [test & body]

  `(if ~test nil (do ~@body)))


(defmacro
  ^{ :arglists '("(coalesce args*)")
     :doc """
          Returns nil if all of its arguments are nil, otherwise it returns
          the first non nil argument. The arguments are evaluated lazy.
          """
     :examples '(
         "(coalesce)",
         "(coalesce 2)"
         "(coalesce nil 1 2)") }

  coalesce
    ([] nil)
    ([x] x)
    ([x & rest] `(let [c# ~x] (if (not (nil? c#)) c# (coalesce ~@rest)))))


(defmacro
  ^{ :arglists '(
          "(if-let bindings then)"
          "(if-let bindings then else)")
     :doc """
          bindings is a vector with 2 elements: binding-form test.¶
          If test is true, evaluates then with binding-form bound to the value
          of test, if not, yields else
          """
     :examples '(
          """
          (if-let [value (* 100 2)]
            (str "The expression is true. value=" value)
            (str "The expression is false."))
          """ )
     :see-also '("when-let" "let") }

  if-let

  ([bindings then]
     `(if-let ~bindings ~then nil))
  ([bindings then else]
    (let [form (first bindings) tst (second bindings)]
      `(let [temp# ~tst]
        (if temp#
          (let [~form temp#]
            ~then)
          ~else)))))


(defmacro
  ^{ :arglists '("(when-let bindings & body)")
     :doc """
          bindings is a vector with 2 elements: binding-form test.¶
          If test is true, evaluates the body expressions with binding-form
          bound to the value of test, if not, yields nil
          """
     :examples '(
          """
          (when-let [value (* 100 2)]
            (str "The expression is true. value=" value))
          """ )
     :see-also '("if-let" "let") }

  when-let [bindings & body]

  (let [form (first bindings) tst (second bindings)]
    `(let [temp# ~tst]
      (when temp#
        (let [~form temp#]
          ~@body)))))


(defmacro
  ^{ :arglists '("(dotimes bindings & body)")
     :doc "Repeatedly executes body with name bound to integers from 0 through n-1."
     :examples '("""(dotimes [n 3] (println (str "n is " n)))""" )
     :see-also '("repeat" "repeatedly" "doseq" "list-comp") }

  dotimes [bindings & body]

  (let [i (first bindings)
        n (second bindings)]
      `(let [k# (long ~n)]
          (loop [~i 0]
            (when (< ~i k#)
                ~@body
                (recur (inc ~i)))))))


(defmacro
  ^{ :arglists '("(while test & body)")
     :doc """
          Repeatedly executes body while test expression is true. Presumes some
          side-effect will cause test to become false/nil. Returns nil.
          """
     :examples '(
          """
          (do
            (def a (atom 5))
            (while (pos? @a)
              (println @a)
              (swap! a dec)))
          """ ) }

  while [test & body]

  `(loop []
      (when ~test
        ~@body
        (recur))))


(defmacro
  ^{ :arglists '("(doto x & forms)")
     :doc """
          Evaluates x then calls all of the methods and functions with the value
          of x supplied at the front of the given arguments. The forms are
          evaluated in order. Returns x.
          """
     :examples '(
          """
          (doto (. :java.util.HashMap :new)
                (. :put :a 1)
                (. :put :b 2))
          """ ) }

  doto [x & forms]

  (let [gx (gensym)]
    `(let [~gx ~x]
       ~@(map (fn [f] `(~(first f) ~gx ~@(rest f))) forms)
       ~gx)))


(defmacro
  ^{ :arglists '("(-> x & forms)")
     :doc """
          Threads the x through the forms. Inserts x as the second item in the
          first form, making a list of it if it is not a list already.¶
          If there are more forms, inserts the first form as the second item in
          second form, etc.
          """
     :examples '(
          """
          (-> 5 (+ 3) (/ 2) (- 1))
          """
          """
          (do
            (def person
                 {:name "Peter Meier"
                  :address {:street "Lindenstrasse 45"
                            :city "Bern"
                            :zip 3000}})

            (-> person :address :street))
          """ )
     :see-also '("->>" "-<>" "as->") }

  -> [x & forms]

  (loop [x x, forms forms]
    (if forms
      (let [form (first forms)
            threaded (if (list? form)
                       `(~(first form) ~x ~@(empty-to-nil (rest form)))
                       (list form x))]
        (recur threaded (empty-to-nil (rest forms))))
      x)))


(defmacro
  ^{ :arglists '("(->> x & forms)")
     :doc """
          Threads the x through the forms. Inserts x as the last item in the
          first form, making a list of it if it is not a list already. If there
          are more forms, inserts the first form as the last item in second
          form, etc.
          """
     :examples '(
          "(->> 5 (+ 3) (/ 32) (- 1))"
          """
          (->> [ {:a 1 :b 2} {:a 3 :b 4} {:a 5 :b 6} {:a 7 :b 8} ]
               (map (fn [x] (get x :b)))
               (filter (fn [x] (> x 4)))
               (map inc))))
          """ )
     :see-also '("->" "-<>" "as->") }

  ->> [x & forms]

   (loop [x x, forms forms]
     (if forms
       (let [form (first forms)
             threaded (if (list? form)
                         `(~(first form) ~@(empty-to-nil (rest form)) ~x)
                         (list form x))]
         (recur threaded (empty-to-nil (rest forms))))
       x)))


(defmacro
  ;; serves the macro -<>. Inserts x in place of '<>' in form

  -<>* [form x]

  (let [substitute-pos (fn [form_] (replace {'<> x} form_))
        count-pos (fn [form_] (count (filter #(== % '<>) form_)))
        c (cond
            (or (set? form) (list? form) (vector? form)) (count-pos form)
            (map? form) (count-pos (mapcat concat form))
            :otherwise 0)]
    (cond
      (not (== c 1))   (throw (ex :VncException "-<> macro: one position per form is required."))
      (symbol? form)   `(~form ~x)
      (vector? form)   (substitute-pos form)
      (map? form)      (apply hash-map (mapcat substitute-pos form))
      :else            `(~(first form) ~@(substitute-pos (rest form))))))

(defmacro
  ^{ :arglists '("(-<> x & forms)")
     :doc """
          Threads the x through the forms. Inserts x at position of the `<>`
          symbol of the first form, making a list of it if is not a list
          already. If there are more forms, inserts the first form at position
          of the `<>` symbol in second form, etc.
          """
     :examples '(
          """
          (-<> 5
               (+ <> 3)
               (/ 2 <>)
               (- <> 1))
          """ )
     :see-also '("->" "->>" "as->") }

  -<>

  ([x] x)
  ([x form] `(-<>* ~form ~x))
  ([x form & forms] `(-<> (-<> ~x ~form) ~@forms)))


(defmacro
  ^{ :arglists '("(as-> expr name & forms)")
     :doc """
          Binds name to expr, evaluates the first form in the lexical context
          of that binding, then binds name to that result, repeating for each
          successive form, returning the result of the last form. This allows a
          value to thread into any argument position.
          """
     :examples '(
          """
          ; allows to use arbitrary positioning of the argument
          (as-> [:foo :bar] v
                (map name v)
                (first v)
                (str/subs v 1))
          """
          """
          ; allows the use of if statements in the thread
          (as-> {:a 1 :b 2} m
                (update m :a #(+ % 10))
                (if true
                  (update m :b #(+ % 10))
                  m))
          """ )
     :see-also '("->" "->>" "-<>") }

  as-> [expr name & forms]

  `(let [~name ~expr
         ~@(interleave (repeat (count forms) name) (butlast forms))]
      ~(if (empty? forms)
           name
           (last forms))))


(defmacro
  ^{ :arglists '("(cond-> expr & clauses)")
     :doc """
          Takes an expression and a set of test/form pairs. Threads expr
          (via `->`) through each form for which the corresponding test
          expression is true. Note that, unlike cond branching, `cond->`
          threading does not short circuit after the first true test
          expression.

          It is useful in situations where you want selectively assoc,
          update, or dissoc something from a map.

          ```venice
          (cond-> m
            (some-pred? q)  (assoc :key :value))
          ```
          """
     :examples '(
          """
          (cond-> 1        ; we start with 1
            true inc       ; the condition is true so (inc 1) => 2
            false (* 42)   ; the condition is false so the operation is skipped
            (= 2 2) (* 3)) ; (= 2 2) is true so (* 2 3) => 6
          """ )
     :see-also '("cond->>") }

  cond-> [expr & clauses]

  (assert (even? (count clauses)))
  (let [g (gensym)
        steps (map (fn [[test step]] `(if ~test (-> ~g ~step) ~g))
                   (partition 2 clauses))]
    `(let [~g ~expr
           ~@(interleave (repeat (count steps) g) (butlast steps))]
       ~(if (empty? steps)
          g
          (last steps)))))

(defmacro
  ^{ :arglists '("(cond->> expr & clauses)")
     :doc """
          Takes an expression and a set of test/form pairs. Threads expr
          (via `->>`) through each form for which the corresponding test
          expression is true.  Note that, unlike cond branching, `cond->>`
          threading does not short circuit after the first true test
          expression.
          """
     :examples '(
          """
          (cond->> 1       ; we start with 1
            true inc       ; the condition is true so (inc 1) => 2
            false (* 42)   ; the condition is false so the operation is skipped
            (= 2 2) (* 3)) ; (= 2 2) is true so (* 3 2) => 6
          """ )
     :see-also '("cond->") }

  cond->> [expr & clauses]

  (assert (even? (count clauses)))
  (let [g (gensym)
        steps (map (fn [[test step]] `(if ~test (->> ~g ~step) ~g))
                   (partition 2 clauses))]
    `(let [~g ~expr
           ~@(interleave (repeat (count steps) g) (butlast steps))]
       ~(if (empty? steps)
          g
          (last steps)))))

(defmacro
  ^{ :arglists '("(some-> expr & forms)")
     :doc """
          When expr is not nil, threads it into the first form (via `->`),
          and when that result is not nil, through the next etc.
          """
     :examples '(
          """
          (some-> {:y 3 :x 5}
                  :y
                  (- 2))
          """
          """
          (some-> {:y 3 :x 5}
                  :z
                  (- 2))
          """ )
     :see-also '("some->>") }

  some-> [expr & forms]

  (let [g (gensym)
        steps (map (fn [step] `(if (nil? ~g) nil (-> ~g ~step)))
                   forms)]
    `(let [~g ~expr
           ~@(interleave (repeat g) (butlast steps))]
       ~(if (empty? steps)
          g
          (last steps)))))


(defmacro
  ^{ :arglists '("(some->> expr & forms)")
     :doc """
          When expr is not nil, threads it into the first form (via `->>`),
          and when that result is not nil, through the next etc.
          """
     :examples '(
          """
          (some->> {:y 3 :x 5}
                   :y
                   (- 2))
          """
          """
          (some->> {:y 3 :x 5}
                   :z
                   (- 2))
          """ )
     :see-also '("some->") }

  some->> [expr & forms]

  (let [g (gensym)
        steps (map (fn [step] `(if (nil? ~g) nil (->> ~g ~step)))
                   forms)]
    `(let [~g ~expr
           ~@(interleave (repeat g) (butlast steps))]
       ~(if (empty? steps)
          g
          (last steps)))))


(defmacro
  ^{ :arglists '("(list-comp seq-exprs body-expr)")
     :doc """
          List comprehension. Takes a vector of one or more binding-form or
          collection-expr pairs, each followed by zero or more modifiers, and
          yields a collection of evaluations of expr.

          Supported modifiers are: `:when` predicate
          """
     :examples '(
          "(list-comp [x (range 10)] x)"
          "(list-comp [x (range 5)] (* x 2))"
          "(list-comp [x (range 10) :when (odd? x)] x)"
          "(list-comp [x (range 10) :when (odd? x)] (* x 2))"
          "(list-comp [x (seq \"abc\") y [0 1 2]] [x y])")
     :see-also '("doseq" "dotimes") }

  list-comp [[binding_ coll-expr & bindings] body-expr]

  (cond
    (not binding_)
      `(list ~body-expr)

    (== :when binding_)
      `(when ~coll-expr (list-comp ~bindings ~body-expr))

    :else
      `(mapcat (fn [~binding_] (list-comp ~bindings ~body-expr))
                ~coll-expr)))


(defmacro
  ^{ :arglists '("(doseq seq-exprs & body)")
     :doc """
          Repeatedly executes body (presumably for side-effects) with
          bindings and filtering as provided by `list-comp`. Does not
          retain the head of the sequence. Returns `nil`.

          Supported modifiers are: `:when` predicate
          """
     :examples '(
          "(doseq [x (range 10)] (print x))"
          "(doseq [x (range 10)] (print x) (print \"-\"))"
          "(doseq [x (range 5)] (print (* x 2)))"
          "(doseq [x (range 10) :when (odd? x)] (print x))"
          "(doseq [x (range 10) :when (odd? x)] (print (* x 2)))"
          "(doseq [x [1 2 3] y [1 2 3]] (println [x y]))")
     :see-also '("list-comp" "dotimes") }

  doseq [[binding_ coll-expr & bindings] & body]

  (cond
    (not binding_)
      `(do ~@body nil)

    (== :when binding_)
      `(when ~coll-expr (doseq ~bindings ~@body) nil)

    :else
      `(do
         (map (fn [~binding_] (doseq ~bindings ~@body)) ~coll-expr)
         nil)))


(defmacro
  ^{ :arglists '("(time expr)")
     :doc "Evaluates expr and prints the time it took. Returns the value of expr."
     :examples '("(time (+ 100 200))")
     :see-also '("perf" "prof") }

  time [expr]

  `(let [start# (nano-time)
         ret# ~expr
         end# (nano-time)
         elapsed# (- end# start#)]
     (printf "Elapsed time: %s%n" (format-nano-time elapsed# :precision 2))
     ret#))


(defmacro
  ^{ :arglists '("(perf expr warmup-iterations test-iterations)")
     :doc """
          Performance test with the given expression.

          Runs the test in 3 phases:

             1. Runs the expr in a warmup phase to allow the HotSpot compiler
                to do optimizations.
             2. Runs the garbage collector.
             3. Runs the expression under profiling. Returns nil.

          After a test run metrics data can be obtained with (prof :data-formatted)
          """
     :examples '(
          """
          (do
            (perf (+ 120 200) 12000 1000)
            (println (prof :data-formatted)))
          """ )
     :see-also '("time" "prof") }

  perf [expr warmup-iterations test-iterations]

  `(do
     (prof :on)
     (prof :clear)
     (let [fn-perf-warmup# (fn _warmup [] (dorun ~warmup-iterations ~expr))
           fn-perf-gc# (fn _warmup-gc [] (do (dorun 2 (gc)) (sleep 1000)))
           fn-perf-test# (fn _test [] (dorun ~test-iterations ~expr))]
       (fn-perf-warmup#)
       (fn-perf-gc#)
       (prof :clear)
       (fn-perf-test#)
       (prof :off))
     nil))


(defmacro
  ^{ :arglists '("(load-string s)")
     :doc "Sequentially read and evaluate the set of forms contained in the string."
     :examples '(
          """
          (do
            (load-string "(def x 1)")
            (+ x 2))
          """ )
     :see-also '("load-file" "load-classpath-file")  }

  load-string [s]

  `(let [parsed# (read-string (str "(do " ~s ")"))
         expanded# (if core/*macroexpand-on-load*
                     (macroexpand-all parsed#)
                     parsed#)]
     (eval expanded#)))


(defmacro
  ^{ :arglists '(
          "(load-file file)",
          "(load-file file force)")
     :doc """
          Sequentially read and evaluate the set of forms contained in the file.

          If the file can not be found on the global load paths and
          and the sandbox permits the file is either loaded from the current
          working directory if it has a relative path or it is loaded from its
          absolute path.

          With 'force' set to false (the default) the file is only loaded once
          and then served from a cache. With 'force' set to true it is always
          loaded physically.

          The function is restricted to load files with the extension '.venice'.
          If the file extension is missing '.venice' will be implicitely added.
          Returns 'true' if the file has been successfully loaded and 'false' if
          the file has been already loaded. Throws an exception on loading error.
          """
     :examples '(
          """(load-file "coffee")""",
          """(load-file "coffee.venice")""",
          """(load-file "beverages/coffee")""" )
     :see-also '("load-classpath-file" "load-string") }

  load-file

  ([file]
    `(load-file ~file false))

  ([file force]
    `(let [fc# (if (str/ends-with? (name ~file) ".venice")
                   (name ~file)
                   (str (name ~file) ".venice"))]
       (if (or ~force (not-contains? core/*loaded-files* fc#))
         (do
           (let [code# (core/load-file* fc#)]
             (when (some? code#)
               (let [parsed# (read-string (str "(do " code# ")") fc#)
                     expanded# (if core/*macroexpand-on-load*
                                 (macroexpand-all parsed#)
                                 parsed#)]
                 (cons! fc# core/*loaded-files*)
                 (eval expanded#)
                 fc#)))
            true)
        false))))


(defmacro
  ^{ :arglists '(
          "(load-classpath-file name)",
          "(load-classpath-file name force)")
     :doc """
          Sequentially read and evaluate the set of forms contained in the
          classpath file. The function is restricted to classpath files with
          the extension '.venice'.
          """
     :examples '(
          """
          (do
            (load-classpath-file "com/github/jlangch/venice/test.venice")
            (test/test-fn "hello"))
          """,
          """
          (do
            (load-classpath-file "com/github/jlangch/venice/test.venice")
            (test/test-fn "hello")
            ; reload the classpath file
            (ns-remove test)
            (load-classpath-file "com/github/jlangch/venice/test.venice" true)
            (test/test-fn "hello"))
          """ )
     :see-also '("load-file" "load-string") }

  load-classpath-file

  ([file]
    `(load-classpath-file ~file false))

  ([file force]
    `(let [fc# (if (str/ends-with? (name ~file) ".venice")
                   (name ~file)
                   (str (name ~file) ".venice"))]
       (when (or ~force (not-contains? core/*loaded-files* fc#))
         (let [code# (core/load-classpath-file* fc#)]
           (when (some? code#)
             (let [parsed# (read-string (str "(do " code# ")") fc#)
                   expanded# (if core/*macroexpand-on-load*
                               (macroexpand-all parsed#)
                               parsed#)]
               (cons! fc# core/*loaded-files*)
               (eval expanded#)
               fc#)))))))


(defmacro
  ^{ :arglists '(
          "(load-module m)",
          "(load-module m force)")
     :doc """
          Loads a Venice predefined extension module.¶
          Returns `true` if the module has been successfully loaded and `false`
          if the module has been already loaded. Throws an exception on loading
          error.
          """
     :examples '(
          """
          (load-module :trace)
          """,
          """
          ;; reloading a module
          (do
            (load-module :trace)
            ; reload the module
            (ns-remove trace)
            (load-module :trace true))
          """) }

  load-module

  ([m]
    `(load-module ~m false))

  ([m force]
    `(when (or ~force (not-contains? core/*loaded-modules* (keyword (name ~m))))
       (let [code# (str "(do " (core/load-module* ~m) ")")
             parsed# (read-string code# ~m)
             expanded# (if core/*macroexpand-on-load*
                         (macroexpand-all parsed#)
                         parsed#)]
         (cons! (keyword (name ~m)) core/*loaded-modules*)
         (eval expanded#)
         (keyword (name ~m))))))


(defmacro
  ^{ :arglists '("(with-sh-dir dir & forms)")
     :doc "Sets the directory for use with sh, see sh for details."
     :examples '("""(with-sh-dir "/tmp" (sh "ls" "-l"))""")
     :see-also '("sh", "with-sh-env", "with-sh-throw") }

  with-sh-dir [dir & forms]

  `(binding [*sh-dir* ~dir] (do ~@forms)))


(defmacro
  ^{ :arglists '("(with-sh-env env & forms)")
     :doc "Sets the environment for use with `sh`."
     :examples '("""(with-sh-env {"NAME" "foo"} (sh "ls" "-l"))""")
     :see-also '("sh", "with-sh-dir", "with-sh-throw") }

  with-sh-env [env & forms]

  `(binding [*sh-env* ~env] (do ~@forms)))


(defmacro
  ^{ :arglists '("(with-sh-throw forms)")
     :doc """
          Shell commands executed within a `with-sh-throw` context throw an
          exception if the spawned shell process returns an exit code other
          than 0.

          For use with `sh`, see sh for details. `with-sh-throw` can be
          nested.
          """
     :examples '("""(with-sh-throw (sh "ls" "-l"))""")
     :see-also '("sh", "with-sh-env", "with-sh-dir") }

  with-sh-throw [& forms]

  `(binding [*sh-throw-ex* true] (do ~@forms)))


(defmacro
  ^{ :arglists '("(with-out-str & forms)")
     :doc """
          Evaluates exprs in a context in which `*out*` is bound to a capturing
          output stream. Returns the string created by any nested printing
          calls. `with-out-str` can be nested.
          """
     :examples '("""(with-out-str (println "a string"))""")
     :see-also '("with-err-str") }

  with-out-str [& forms]

  `(binding [*out* (. :com.github.jlangch.venice.util.CapturingPrintStream :new)]
     (do ~@forms)
     (. *out* :getOutput)))


(defmacro
  ^{ :arglists '("(with-err-str & forms)")
     :doc """
          Evaluates exprs in a context in which `*err*` is bound to a capturing
          output stream. Returns the string created by any nested printing
          calls. `with-err-str` can be nested.
          """
     :examples '("""(with-err-str (println *err* "a string"))""")
     :see-also '("with-out-str") }

  with-err-str [& forms]

  `(binding [*err* (. :com.github.jlangch.venice.util.CapturingPrintStream :new)]
     (do ~@forms)
     (. *err* :getOutput)))


(defmacro
  ^{ :arglists '("(delay & body)")
     :doc """
          Takes a body of expressions and yields a Delay object that will invoke
          the body only the first time it is forced (with `force` or
          `deref` / `@`), and will cache the result and return it on all
          subsequent force calls.
          """
     :examples '(
          """
          (do
            (def x (delay (println "working...") 100))
            (deref x))
          """)
    :see-also '("deref" "force" "realized?" "delay?" "memoize") }

  delay [& body]

  `(. :com.github.jlangch.venice.impl.types.concurrent.Delay
      :new
      (fn [] (do ~@body))))


(defn
  ^{ :arglists '("(print & xs)", "(print os & xs)")
     :doc """
          Without output stream prints to the output stream that is the current
          value of `*out*`. With no args, prints the empty string. With one arg x,
          prints x.toString(). With more than one arg, prints the concatenation
          of the string values of the args with delimiter ' '. With an
          output stream prints to that output stream.¶
          Returns nil.
          """
     :examples '(
         "(print [10 20 30])",
         "(print *out* [10 20 30])",
         "(print *err* [10 20 30])" )
     :see-also '("println" "printf" "newline") }

  print

  ([] nil)
  ([x] (when-not (instance-of? :java.io.PrintStream x)
         (. *out* :append (str (coalesce x "nil")))
         nil))
  ([& xs] (do
            (if (instance-of? :java.io.PrintStream (first xs))
              (. (first xs) :append (str/join " " (map #(str (coalesce % "nil")) (rest xs))))
              (. *out* :append (str/join " " (map #(str (coalesce % "nil")) xs))))
            nil)))


(defn
  ^{ :arglists '("(printf fmt & args)", "(printf os fmt & args)")
     :doc """
          Without output stream prints formatted output as per format to the
          output stream that is the current value of `*out*`.
          With an output stream prints to that output stream.¶
          Returns nil.
          """
     :examples '(
          """(printf "%s: %d" "abc" 100)"""
          """(printf "line 1: %s%nline 2: %s%n" "123" "456")"""
          """(printf "%d%%" 42)"""
          """(printf *out* "%s: %d" "abc" 100)"""
          """(printf *err* "%s: %d" "abc" 100)""" )
      :see-also '("print" "println" "newline") }

  printf [& xs]
    (if (instance-of? :java.io.PrintStream (first xs))
       (let [os (first xs), fmt (second xs), args (rest (rest xs))]
         (print os (apply str/format fmt args)))
       (let [fmt (first xs), args (rest xs)]
         (print *out* (apply str/format fmt args))))
    nil)


(defn
  ^{ :arglists '("(println & xs)", "(println os & xs)")
     :doc """
          Without output stream prints to the output stream that is the current
          value of `*out*` with a tailing linefeed. With no args, prints the
          empty string. With one arg x, prints x.toString(). With more than one
          arg, prints the concatenation of the string values of the args with
          delimiter ' '. With an output stream prints to that output stream.¶
          Returns nil.
          """
     :examples '(
          "(println 200)"
          "(println [10 20 30])"
          "(println *out* 200)"
          "(println *err* 200)" )
      :see-also '("print" "printf" "newline") }

  println

  ([] (newline))
  ([& xs]
    (if (instance-of? :java.io.PrintStream (first xs))
      (do
        (apply print xs)
        (newline (first xs)))
      (do
        (apply print xs)
        (newline)))
    nil))


(defn
  ^{ :arglists '("(newline)", "(newline os)")
     :doc """
          Without arg writes a platform-specific newline to the output stream
          that is the current value of `*out*`. With arg writes a newline to the
          passed output stream.¶
          Returns nil.
          """
     :examples '(
          "(newline)"
          "(newline *out*)"
          "(newline *err*)" )
      :see-also '("print" "println" "printf") }

  newline

  ([] (newline *out*))
  ([os] (. os :append *newline*) nil))


(defn
  ^{ :arglists '("(flush)", "(flush os)")
     :doc """
          Without arg flushes the output stream that is the current value
          of `*out*`. With arg flushes the passed output stream.¶
          Returns nil.
          """
     :examples '(
          "(flush)"
          "(flush *out*)"
          "(flush *err*)" ) }

  flush

  ([] (flush *out*))
  ([os] (. os :flush) nil))


(defn
  ^{ :arglists '("(read-line)")
     :doc """
          Reads the next line from the stream that is the current value of
          `*in*`.¶
          Returns nil if the end of the stream is reached.
          """
     :see-also '("read-char")  }

  read-line  []

  (try
    (. *in* :readLine)
    (catch :java.io.IOException e (ex :VncException (:message e)))))


(defn
  ^{ :arglists '("(read-char)")
     :doc """
          Reads the next char from the stream that is the current value of
          `*in*`.¶
          Returns nil if the of the stream is reached.
          """
     :see-also '("read-line") }

  read-char  []

  (try
    (let [ch (. *in* :read)]
      (if (= -1I ch) nil (char ch)))
    (catch :java.io.IOException e (ex :VncException (:message e)))))


(defn
  ^{ :arglists '("(complement f)")
     :doc """
          Takes a fn f and returns a fn that takes the same arguments as f,
          has the same effects, if any, and returns the opposite truth value.
          """
     :examples '("(complement even?)"
                 "(filter (complement even?) '(1 2 3 4))") }

  complement

  [f] (fn
         ([] (not (f)))
         ([x] (not (f x)))
         ([x y] (not (f x y)))
         ([x y & zs] (not (apply f x y zs)))))


(defn
  ^{ :arglists '("(rf-first)")
     :doc "Returns a reducing function for a transducer that returns the first item."
     :examples '(
          "(transduce (filter number?) rf-first [false 1 2])"
          "(transduce identity rf-first [nil 1 2])")
     :see-also '(
          "rf-last" "rf-any?" "rf-every?") }

  rf-first

  ([] nil)
  ([x] x)
  ([x y] (reduced y)))


(defn
  ^{ :arglists '("(rf-last)")
     :doc "Returns a reducing function for a transducer that returns the last item."
     :examples '(
          "(transduce (filter number?) rf-last [false 1 2])"
          "(transduce identity rf-last [1 2 1.2])")
     :see-also '(
          "rf-first" "rf-any?" "rf-every?") }

  rf-last

  ([] nil)
  ([x] x)
  ([x y] y))


(defn
  ^{ :arglists '("(rf-any? pred)")
     :doc """
          Returns a reducing function for a transducer that returns true if the
          predicate is true for at least one the items, false otherwise.
          """
     :examples '(
          "(transduce (filter number?) (rf-any? pos?) [true -1 1 2 false])")
     :see-also '(
          "rf-first" "rf-last" "rf-every?") }

  rf-any?

  [pred] (fn
           ([] false)
           ([x] x)
           ([x y] (if (pred y) (reduced true) x))))


(defn
  ^{ :arglists '("(rf-every? pred)")
     :doc """
          Returns a reducing function for a transducer that returns true if the
          predicate is true for all the items, false otherwise.
          """
     :examples '(
          "(transduce (filter number?) (rf-every? pos?) [1 2 3])")
     :see-also '(
          "rf-first" "rf-last" "rf-any?") }

  rf-every?

  [pred] (let [empty_ (volatile true)]
            (fn
              ([] false)
              ([x] x)
              ([x y] (do
                       (let [r (if @empty_ (pred y) (and x (pred y)))]
                         (reset! empty_ false)
                         (if r r (reduced r))))))))


(defn
  ^{ :arglists '("(constantly x)")
     :doc """
          Returns a function that takes any number of arguments and returns
          always the value x.
          """
     :examples '(
          """
          (do
            (def fix (constantly 10))
            (fix 1 2 3)
            (fix 1)
            (fix ))
          """)
     :see-also '("repeat" "repeatedly" "dotimes")  }

  constantly

  [x] (fn [& args] x))


(defn
  ^{ :arglists '("(zipmap keys vals)")
     :doc """
          Returns a map with the keys mapped to the corresponding vals.

          To create a list of tuples from two or more lists use¶
          `(map list '(1 2 3) '(4 5 6))`.
          """
     :examples '(
          "(zipmap [:a :b :c :d :e] [1 2 3 4 5])"
          "(zipmap [:a :b :c] [1 2 3 4 5])") }

  zipmap [keys vals]

  (loop [omap (ordered-map)
         ks (seq keys)
         vs (seq vals)]
     (if (and (not-empty? ks) (not-empty? vs))
        (recur (assoc omap (first ks) (first vs))
               (rest ks)
               (rest vs))
        omap)))


(defn
  ^{ :arglists '("(memoize f)")
     :doc """
          Returns a memoized version of a referentially transparent function.

          Note:¶
          Use memoization for expensive calculations. If used with fast
          calculations it has the opposite effect and can slow it down
          actually!
          """
     :examples '(
          """
          (do
            (def fibonacci
              (memoize
                (fn [n]
                  (cond
                    (<= n 0) 0
                    (< n 2) 1
                    :else (+ (fibonacci (- n 1)) (fibonacci (- n 2)))))))

            (time (fibonacci 25)))
          """,
          """
          (do
            (defn test [a b]
              (println (str "calculating a=" a ", b=" b))
              (+ a b))

            (def test-memo (memoize test))

            (test-memo 1 1)
            (test-memo 1 2)
            (test-memo 1 1)
            (test-memo 1 2)
            (test-memo 1 1))
          """ )
     :see-also '("delay") }

  memoize

  [f]
    (let [mem_ (atom {})]
      (fn [& args]
        (if-let [e (find @mem_ args)]
          (val e)
          (let [ret (apply f args)]
            (swap! mem_ assoc args ret)
            ret)))))


(defn
  ^{ :arglists '("(trampoline f)" "(trampoline f & args)")
     :doc """
          trampoline can be used to convert algorithms requiring mutual
          recursion without stack consumption. Calls f with supplied args,
          if any. If f returns a fn, calls that fn with no arguments, and
          continues to repeat, until the return value is not a fn, then
          returns that non-fn value.

          Note that if you want to return a fn as a final value, you must
          wrap it in some data structure and unpack it after trampoline
          returns.
          """
     :examples '(
          """
          (do
            (defn factorial
              ([n] #(factorial n 1N))
              ([n acc] (if (< n 2)
                       acc
                       #(factorial (dec n) (* acc n)))))

            (trampoline (factorial 20)))
          """ ) }

  trampoline

  ([f]
    (loop [f f]
      (let [ret (f)]
        (if (fn? ret)
          (recur ret)
          ret))))
  ([f & args]
     (trampoline #(apply f args))))


(defn
  ^{ :arglists '("(load-resource res)")
     :doc """
          Loads a resource from the application archive or the `*load-path*`.
          Returns a bytebuffer or nil if the resource is not found in any
          of the two locations.
          """ }

  load-resource [res]

  (load-resource* res *load-path* :binary true))


(defn-
  ^{ :arglists '("(walk inner outer form)")
     :doc """
          Traverses form, an arbitrary data structure. inner and outer are
          functions. Applies inner to each element of form, building up a
          data structure of the same type, then applies outer to the result.

          Do not call this function directly use core/postwalk or
          `core/prewalk` instead.
          """ }

  walk [inner outer form]

  (cond
    (list? form) (outer (apply list (map inner form)))
    (map-entry? form) (outer (map-entry (inner (key form)) (inner (val form))))
    (coll? form) (outer (into (empty form) (map inner form)))
    :else (outer form)))


(defn
  ^{ :arglists '("(postwalk f form)")
     :doc """
          Performs a depth-first, post-order traversal of form. Calls f on
          each sub-form, uses f's return value in place of the original.
          """
     :examples '(
          """
          (postwalk (fn [x] (println "Walked:" (pr-str x)) x)
                    '(1 2 {:a 1 :b 2}))
          """)
     :see-also '("prewalk") }

  postwalk [f form]

  (core/walk (partial postwalk f) f form))


(defn
  ^{ :arglists '("(prewalk f form)")
     :doc """
          Performs a depth-last, pre-order traversal of form. Calls f on
          each sub-form, uses f's return value in place of the original.
          """
     :examples '(
          """
          (prewalk (fn [x] (println "Walked:" (pr-str x)) x)
                   '(1 2 {:a 1 :b 2}))
          """)
     :see-also '("postwalk") }

  prewalk [f form]

  (core/walk (partial prewalk f) identity (f form)))


(defn
  ^{ :arglists '("(macroexpand-all form)")
     :doc "Recursively expands all macros in the form."
     :examples '(
          "(macroexpand-all '(and true true))",
          "(macroexpand-all '(and true (or true false) true))"
          "(macroexpand-all '(let [n 5] (cond (< n 0) -1 (> n 0) 1 :else 0)))" )
     :see-also '("macroexpand" "defmacro") }

  macroexpand-all [form]

  ;;(core/prewalk (fn [x] (if (list? x) (macroexpand x) x)) form))
  (macroexpand-all* form))


(defn
  ^{ :arglists '("(as-runnable f)")
     :doc "Wraps the function f in a [java.lang.Runnable](https://docs.oracle.com/javase/8/docs/api/java/lang/Runnable.html)"
     :see-also '("as-callable" "as-predicate" "as-function" "as-consumer" "as-supplier") }

  as-runnable [f] (proxify :java.lang.Runnable { :run f }))


(defn
  ^{ :arglists '("(as-callable f)")
     :doc "Wraps the function f in a [java.util.concurrent.Callable](https://docs.oracle.com/javase/8/docs/api/java/util/concurrent/Callable.html)"
     :see-also '("as-runnable" "as-predicate" "as-function" "as-consumer" "as-supplier") }

  as-callable [f] (proxify :java.util.concurrent.Callable { :call f }))


(defn
  ^{ :arglists '("(as-predicate f)")
     :doc "Wraps the function f in a [java.util.function.Predicate](https://docs.oracle.com/javase/8/docs/api/java/util/function/Predicate.html)"
     :see-also '("as-runnable" "as-callable" "as-function" "as-consumer" "as-supplier") }

  as-predicate [f] (proxify :java.util.function.Predicate { :test f }))


(defn
  ^{ :arglists '("(as-function f)")
     :doc "Wraps the function f in a [java.util.function.Function](https://docs.oracle.com/javase/8/docs/api/java/util/function/Function.html)"
     :see-also '("as-runnable" "as-callable" "as-predicate" "as-consumer" "as-supplier") }

  as-function [f] (proxify :java.util.function.Function { :apply f }))


(defn
  ^{ :arglists '("(as-consumer f)")
     :doc "Wraps the function f in a [java.util.function.Consumer](https://docs.oracle.com/javase/8/docs/api/java/util/function/Consumer.html)"
     :see-also '("as-runnable" "as-callable" "as-predicate" "as-function" "as-supplier") }

  as-consumer [f] (proxify :java.util.function.Consumer { :accept f }))


(defn
  ^{ :arglists '("(as-supplier f)")
     :doc "Wraps the function f in a [java.util.function.Supplier](https://docs.oracle.com/javase/8/docs/api/java/util/function/Supplier.html)"
     :see-also '("as-runnable" "as-callable" "as-predicate" "as-function" "as-consumer") }

  as-supplier [f] (proxify :java.util.function.Supplier { :get f }))


(defn
  ^{ :arglists '("(as-bipredicate f)")
     :doc "Wraps the function f in a [java.util.function.BiPredicate](https://docs.oracle.com/javase/8/docs/api/java/util/function/BiPredicate.html)"
     :see-also '("as-bifunction" "as-biconsumer" "as-binaryoperator") }

  as-bipredicate [f] (proxify :java.util.function.BiPredicate { :apply f }))


(defn
  ^{ :arglists '("(as-bifunction f)")
     :doc "Wraps the function f in a [java.util.function.BiFunction](https://docs.oracle.com/javase/8/docs/api/java/util/function/BiFunction.html)"
     :see-also '("as-bipredicate" "as-biconsumer" "as-binaryoperator") }

  as-bifunction [f] (proxify :java.util.function.BiFunction { :apply f }))


(defn
  ^{ :arglists '("(as-biconsumer f)")
     :doc "Wraps the function f in a [java.util.function.BiConsumer](https://docs.oracle.com/javase/8/docs/api/java/util/function/BiConsumer.html)"
     :see-also '("as-bipredicate" "as-bifunction" "as-binaryoperator") }

  as-biconsumer [f] (proxify :java.util.function.BiConsumer { :apply f }))


(defn
  ^{ :arglists '("(as-binaryoperator f)")
     :doc "Wraps the function f in a [java.util.function.BinaryOperator](https://docs.oracle.com/javase/8/docs/api/java/util/function/BinaryOperator.html)"
     :see-also '("as-bipredicate" "as-bifunction" "as-biconsumer" ) }

  as-binaryoperator [f] (proxify :java.util.function.BinaryOperator { :apply f }))
