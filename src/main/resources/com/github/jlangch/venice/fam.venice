;;;;   __    __         _
;;;;   \ \  / /__ _ __ (_) ___ ___
;;;;    \ \/ / _ \ '_ \| |/ __/ _ \
;;;;     \  /  __/ | | | | (_|  __/
;;;;      \/ \___|_| |_|_|\___\___|
;;;;
;;;;
;;;; Copyright 2017-2024 Venice
;;;;
;;;; Licensed under the Apache License, Version 2.0 (the "License");
;;;; you may not use this file except in compliance with the License.
;;;; You may obtain a copy of the License at
;;;;
;;;;     http://www.apache.org/licenses/LICENSE-2.0
;;;;
;;;; Unless required by applicable law or agreed to in writing, software
;;;; distributed under the License is distributed on an "AS IS" BASIS,
;;;; WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
;;;; See the License for the specific language governing permissions and
;;;; limitations under the License.

;;;; Functors - Applicatives - Monads
;;;;
;;;; See: http://adit.io/posts/2013-04-17-functors,_applicatives,_and_monads_in_pictures.html
;;;;
;;;; http://leonardoborges.com/writings/2012/11/30/monads-in-small-bites-part-i-functors/
;;;; https://hackernoon.com/9-functional-programming-concepts-everyone-should-know-uy503u21
;;;;
;;;; Fluokitten:
;;;;    https://fluokitten.uncomplicate.org/articles/functors_applicatives_monads_in_pictures.html
;;;;    https://github.com/uncomplicate/fluokitten/
;;;;    https://fluokitten.uncomplicate.org/

(ns fam)


(defn- box-type [x]
  (cond
    (nil? x)      :fam.Nil
    (just? x)     :fam.Just
    (list? x)     :fam.List
    (vector? x)   :fam.Vector
    (set? x)      :fam.Set
    (map? x)      :fam.Map
    (atom? x)     :fam.Atom
    (volatile? x) :fam.Volatile
    (fn? x)       :fam.Function
    :else         (type x)))



;; -----------------------------------------------------------------------------
;; Functors
;;
;; A Functor is any type that defines how fmap applies to it.
;;
;; Functors are just abstractions that wrap a value in a context and allow
;; mappings over this value. Mapping means applying a function to a value to
;; get another value.
;; Functors facilitate function composition. They are agnostic of the type
;; inside them so transformation functions can be applied sequentially.
;;
;; The types nil, just, list, set, map vector, atom, volatile, and
;; function are functors.
;; When fmap is used on a function, it's just doing function composition!
;;
;; functors:  applying a function to a wrapped value using fmap
;;
;; fmap:      takes a function and a functor and returns a functor
;;
;; Rules:     1) Functors must preserve identity:
;;                  fmap id == id
;;                  (fmap f functor) == functor
;;            2) Functors must preserve composition:
;;                  fmap (f . g) == fmap f . fmap g
;;                  (fmap (comp f g) functor) == ((comp (partial fmap f) (partial fmap g)) functor)
;;
;; example:   (fmap inc [1 2 3])                             ; => [2 3 4]
;;            (fmap inc (atom 3))                            ; => (atom 4)
;;            (fmap (partial + 3) (just 2))                  ; => (just 5)
;;            (fmap (partial + 3) nil)                       ; => nil
;;            (fmap (partial + 3) [2 4 6])                   ; => [5 7 9]
;;            ((fmap (partial + 1) (partial * 2)) 4)         ; => 9
;;            (->> (just 2)
;;                 (fmap (partial + 3))
;;                 (fmap (partial * 2)))                     ; => (just 10)
;;
;; functors preserve identity:
;;            (fmap identity (just 5))                       ; => (just 5)
;;
;; functors preserve composition:
;;            (fmap (comp (partial + 1) (partial * 2))
;;                  (just 2))                                ; => (just 5)
;;            ((comp (partial fmap (partial + 1))
;;                   (partial fmap (partial * 2))) (just 2)) ; => (just 5)
;; -----------------------------------------------------------------------------

(defmulti fmap (fn [f functor] (fam/box-type functor)))

(defmethod fmap :fam.Nil [f functor]
  nil)

(defmethod fmap :fam.Just [f functor]
  (just (f @functor)))

(defmethod fmap :fam.List [f functor]
  (map f functor))

(defmethod fmap :fam.Vector [f functor]
  (into [] (map f functor)))

(defmethod fmap :fam.Set [f functor]
  (into #{} (map f functor)))

(defmethod fmap :fam.Map [f functor]
  (into {} (map (fn [[k v]] [k (f v)]) functor)))

(defmethod fmap :fam.Atom [f functor]
  (swap! functor f)
  functor)

(defmethod fmap :fam.Volatile [f functor]
  (swap! functor f)
  functor)

(defmethod fmap :fam.Function [f functor]
  (comp f functor))



;; -----------------------------------------------------------------------------
;; Applicatives (Applicative Functor)
;;
;; An applicative is any type that defines how fapply applies to it.
;;
;; the types nil, just, list, vector, atom, volatile are applicatives
;;
;; applicatives:  applying a wrapped function to a wrapped value using fapply
;;
;; fapply:        takes an applicative and a functor and returns a functor
;;
;; example:       (fapply [inc dec] [1 2 3])                   ; => (2 3 4 0 1 2)
;;                (fapply (just (partial + 3)) (just 2))       ; => (just 5)
;;                (fapply (just (partial + 3)) [1 2 3 4])      ; => [4 5 6 7]
;;                (fapply [(partial * 2) (partial + 3)] [1 2]) ; => [2 4 4 5]
;; -----------------------------------------------------------------------------

(defmulti fapply (fn [applicative functor] (fam/box-type applicative)))

(defmethod fapply :fam.Nil [applicative functor]
  nil)

(defmethod fapply :fam.Just [applicative functor]
  (fam/fmap @applicative functor))

(defmethod fapply :fam.List [applicative functor]
  (flatten (map #(fam/fmap % functor) applicative)))

(defmethod fapply :fam.Vector [applicative functor]
  (flatten (map #(fam/fmap % functor) applicative)))

(defmethod fapply :fam.Atom [applicative functor]
  (fam/fmap @applicative functor))

(defmethod fapply :fam.Volatile [applicative functor]
  (fam/fmap @applicative functor))



;; -----------------------------------------------------------------------------
;; Monads
;;
;; A Monad is any type that defines how bind applies to it.
;;
;; the types nil, just, atom, volatile are monads
;;
;; monads:   applying a function that returns a wrapped value, to a wrapped
;;           value using bind
;;
;; bind:     takes a monad and a function that returns a monad. bind returns a
;;           monad
;;
;; example:  (defn half [x] (if (even? x) (just (/ x 2)) nil)))
;;
;;           (bind (just 3) half)             ; => nil
;;           (bind (just 4) half)             ; => (just 2)
;;           (bind nil half)                  ; => nil
;;           (bind (just 64) half half half)  ; => (just 8)
;;           (bind (just 20) half half half)  ; => nil
;; -----------------------------------------------------------------------------

(defmulti bind (fn [monad & fs] (fam/box-type monad)))

(defmethod bind :fam.Nil [monad & fs]
  nil)

(defmethod bind :fam.Just [monad & fs]
  (if (== 1 (count fs))
    ((first fs) @monad)
    (reduce (fn [m f] (fam/bind m f)) monad fs)))

(defmethod bind :fam.Atom [monad & fs]
  (if (== 1 (count fs))
    ((first fs) @monad)
    (reduce (fn [m f] (fam/bind m f)) monad fs)))

(defmethod bind :fam.Volatile [monad & fs]
  (if (== 1 (count fs))
    ((first fs) @monad)
    (reduce (fn [m f] (fam/bind m f)) monad fs)))
