;;;;   __    __         _
;;;;   \ \  / /__ _ __ (_) ___ ___
;;;;    \ \/ / _ \ '_ \| |/ __/ _ \
;;;;     \  /  __/ | | | | (_|  __/
;;;;      \/ \___|_| |_|_|\___\___|
;;;;
;;;;
;;;; Copyright 2017-2026 Venice
;;;;
;;;; Licensed under the Apache License, Version 2.0 (the "License");
;;;; you may not use this file except in compliance with the License.
;;;; You may obtain a copy of the License at
;;;;
;;;;     http://www.apache.org/licenses/LICENSE-2.0
;;;;
;;;; Unless required by applicable law or agreed to in writing, software
;;;; distributed under the License is distributed on an "AS IS" BASIS,
;;;; WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
;;;; See the License for the specific language governing permissions and
;;;; limitations under the License.

;;;; REPL setup


(ns repl-setup)

(load-module :installer)
(load-module :jansi-install)
(load-module :maven)
(load-module :ansi)


(def initial-modules [ :jansi-install ])

(def scripts [ "chart.venice"
               "chart-swing.venice"
               "dining-philosophers.venice"
               "expr-parser-1.venice"
               "expr-parser-2.venice"
               "expr-parser-3.venice"
               "fibonacci.venice"
               "sudoku.venice"
               "tomcat-geoip-map.venice"
               "zip-tomcat-logs.venice"
               "webapp/demo-webapp.venice"
               "webapp/rest-webapp.venice"
               "webapp/two-servlet-webapp.venice"
               "webapp/sse-webapp.venice"
               "webapp/async-webapp.venice"
               "webapp/login-webapp.venice"
               "webapp/fileserve-webapp.venice"
               "webapp/upload-webapp.venice"
               "pdf/pdf-example.venice"
               "pdf/pdf-fonts.venice"
               "pdf/pdf-tables.venice"
               "pdf/pdf-images.venice"
               "pdf/pdf-table-of-content.venice"
               "pdf/pdf-footers.venice"
               "pdf/pdf-watermarks.venice"
               "pdf/pdf-text2pdf.venice"
               "pdf/pdf-merge.venice"
               "pdf/pdf-copy.venice" ])


(defn- cr-lf [text]
  (str/cr-lf text (if (os-type? :windows) :cr-lf :lf)))


(defn install-maven [dir]
  (println "Installing Maven ~{maven/maven-version} locally in the REPL/tools ...")
  (if (io/internet-avail?)
    (maven/install maven/maven-version dir)
    (println *err* "Internet is not available! Maven cannot be downloaded!"))
  (println))


(defn install-module* [m dir silent]
  (let [r (str/strip-end (name m) "-install")]
    (println "Installing ~{r} ...")
    (installer/install-module m :dir dir :silent silent)
    (println "Installing ~{r} done.")))


(defn download-modules-libs [dir modules_ silent]
  (when-not (empty? modules_)
    (println "Downloading modules Java 3rdparty libraries...")
    (if (io/internet-avail?)
       (docoll #(install-module* % dir silent) modules_)
       (println *err* "Internet is not available! Jansi lib cannot be installed!")))
  (println))


(defn setup-scripts [scripts-dir]
  (println "Extracting example scripts...")
  (let [load-script (fn [script]
                      (-<> (io/load-classpath-resource script)
                           (bytebuf-to-string <> :utf-8)
                           (repl-setup/cr-lf <>)))]
    (docoll #(io/spit (io/file scripts-dir %)
                      (load-script %)
                      :encoding (charset-default-encoding))
            scripts)))

        
(defn setup-shebang-demo-script [install-dir scripts-dir]
  (when-not (os-type? :windows) 
    (println "Setting up 'shebang-demo.venice'...")
    (-<> (io/load-classpath-resource "com/github/jlangch/venice/setup/shebang-demo.venice")
         (bytebuf-to-string <> :utf-8)
         (repl-setup/cr-lf <>)
         (str/replace-all <> "{{INSTALL_PATH}}" (io/file-path install-dir))
         (io/spit (io/file scripts-dir "shebang-demo.venice") 
                  <> 
                  :encoding (charset-default-encoding)))
    (when (io/exists-file? (io/file scripts-dir "shebang-demo.venice"))
      (println "Makeing \"scripts/shebang-demo.venice\" executable...")
      (sh "/bin/sh" "-c" "chmod u+x shebang-demo.venice" :dir scripts-dir))))


(defn change-repl-launcher-color [cfg color-mode]
  (if (== :none color-mode)
    cfg  ;; do not change
    (let [color-switch (case color-mode
                             :light  "-colors"
                             :dark   "-colors-darkmode"
                                     "-colors")]
      (if (str/contains? cfg "-colors-darkmode")
        (str/replace-last cfg "-colors-darkmode" color-switch)
        (str/replace-last cfg "-colors" color-switch)))))


(defn setup-repl-config [libs-dir]
  (when-not (io/exists-file? (io/file libs-dir "repl.json"))
    (println "Setting up 'repl.json'...")
    (-<> (io/load-classpath-resource "com/github/jlangch/venice/setup/repl.json")
         (bytebuf-to-string <> :utf-8)
         (repl-setup/cr-lf <>)
         (io/spit (io/file libs-dir "repl.json") 
                  <> 
                  :encoding (charset-default-encoding)))))


(defn- setup-repl-launcher-windows [install-dir color-mode]
  (when-not (io/exists-file? (io/file install-dir "repl.env.bat"))
    (println "Setting up 'repl.env.bat'...")
    (-<> (io/load-classpath-resource "com/github/jlangch/venice/setup/repl.win.env")
         (bytebuf-to-string <> :utf-8)
         (repl-setup/cr-lf <>)
         (io/spit (io/file install-dir "repl.env.bat") 
                   <> 
                   :encoding (charset-default-encoding))))
  (when-not (io/exists-file? (io/file install-dir "repl.bat"))
    (println "Setting up 'repl.bat'...")
    (-<> (io/load-classpath-resource "com/github/jlangch/venice/setup/repl.bat")
         (bytebuf-to-string <> :utf-8)
         (repl-setup/cr-lf <>)
         (str/replace-all <> "{{INSTALL_PATH}}" (io/file-path install-dir))
         (change-repl-launcher-color <> color-mode)
         (io/spit (io/file install-dir "repl.bat") 
                  <> 
                  :encoding (charset-default-encoding)))))


(defn- setup-repl-launcher-unix [install-dir color-mode]
  (when-not (io/exists-file? (io/file install-dir "repl.env"))
    (println "Setting up 'repl.env'...")
    (-<> (io/load-classpath-resource "com/github/jlangch/venice/setup/repl.unix.env")
         (bytebuf-to-string <> :utf-8)
         (repl-setup/cr-lf <>)
         (io/spit (io/file install-dir "repl.env") 
                  <> 
                  :encoding (charset-default-encoding))))
  (when-not (io/exists-file? (io/file install-dir "repl.sh"))
    (println "Setting up 'repl.sh'...")
    (-<> (io/load-classpath-resource "com/github/jlangch/venice/setup/repl.sh")
         (bytebuf-to-string <> :utf-8)
         (repl-setup/cr-lf <>)
         (str/replace-all <> "{{INSTALL_PATH}}" (io/file-path install-dir))
         (change-repl-launcher-color <> color-mode)
         (io/spit (io/file install-dir "repl.sh") 
                  <> 
                  :encoding (charset-default-encoding)))
    (when (io/exists-file? (io/file install-dir "repl.sh"))
      (sh "/bin/sh" "-c" "chmod u+x repl.sh" :dir (io/file-path install-dir))
      (when (os-type? :mac-osx)
        (sh "/bin/cp" (io/file install-dir "repl.sh") (io/file install-dir "repl.sh.command"))))))


(defn setup-repl-launcher [install-dir color-mode]
  (if (os-type? :windows)
    (setup-repl-launcher-windows install-dir color-mode)
    (setup-repl-launcher-unix install-dir color-mode)))


(defn- setup-runscript-launcher-unix [install-dir]
  (when-not (io/exists-file? (io/file install-dir "run-script.sh"))
    (println "Setting up 'run-script.sh'...")
    (-<> (io/load-classpath-resource "com/github/jlangch/venice/setup/run-script.sh")
         (bytebuf-to-string <> :utf-8)
         (repl-setup/cr-lf <>)
         (str/replace-all <> "{{INSTALL_PATH}}" (io/file-path install-dir))
         (io/spit (io/file install-dir "run-script.sh") <> :encoding (charset-default-encoding)))
    (when (io/exists-file? (io/file install-dir "run-script.sh"))
      (sh "/bin/sh" "-c" "chmod u+x run-script.sh" :dir (io/file-path install-dir)))))


(defn- setup-runscript-launcher-windows [install-dir]
  )


(defn setup-runscript-launcher [install-dir]
  (if (os-type? :windows)
    (setup-runscript-launcher-windows install-dir)
    (setup-runscript-launcher-unix install-dir)))


(defn copy-venice-jar [libs-dir]
  (let [loc    (java-source-location :com.github.jlangch.venice.Venice)
        name   (when (some? loc) (io/file-name loc))
        target (io/file libs-dir name)]
    (when (str/ends-with? name ".jar")
       (try
         (println (str "Copying Venice JAR '" name "' to libs"))
         (io/copy-file loc target :replace true)
         (catch :Exception e
           (if (io/exists-file? target)
             (println *err*
                      (str "Failed to copy '"
                           name
                           "' to libs. It exists already and can not be overwritten!"))
             (println *err*(str "Failed to copy '" name "' to libs!"))))))))


(defn jansi-available? [libs-dir]
  (let [lib-jansi    (first (jansi-install/libraries))
        jansi-name   (maven/artifact-filename lib-jansi ".jar")
        jansi-jar    (io/file libs-dir jansi-name)]
    (io/exists-file? jansi-jar)))


(defn- print-success-message [install-dir]
  (let [launch-script (if (os-type? :windows) "repl.bat" "./repl.sh")]
    (println )
     (print-msg-box :info
                   """
                   The Venice REPL has been successfully configured and setup. \
                   The original Venice JAR is not needed anymore, it has been \
                   copied to the REPL's 'libs' directory that holds all JAR \
                   files and resources.

                   Start the REPL with:
                   $ cd ~{install-dir}
                   $ ~{launch-script}
                   """)))


(defn- warn-internet-not-available []
  (print-msg-box :warn "Internet is not available! REPL not installed!"))


(defn- warn-install-dir-not-exist [install-dir]
  (print-msg-box :warn
                  """
                  The install dir '~{install-dir}' does not exist! \
                  REPL not installed!
                  """))


(defn- warn-jansi-not-available []
  (print-msg-box :warn
                  """
                  The Jansi JAR could not be downloaded. Without \
                  this JAR the REPL is not fully functional!
                  """))


(defn- warn-internal-error [msg]
  (print-msg-box :warn
                  """
                  Internal error '~{msg}'. REPL not sucessfully installed!
                  """))


(defn venice-maven-url [version]
  (str maven/repo
       "/com/github/jlangch/venice/~{version}/venice-~{version}.jar"))


(defn download-venice-jar [version dir]
  (let [jar-name (str "venice-" version ".jar")
        url      (venice-maven-url version)]
    (->> (io/download url :binary true :user-agent "Mozilla")
         (io/spit (io/file dir jar-name)))))


(defn setup [& options]
  (let [options       (apply hash-map options)
        color-mode    (:color-mode options :none)        ;; {:none :light :dark}
        silent        (not (:ansi-terminal options false))       ;; {true false}
        minimal?      (:minimal options false)                   ;; {true false}
        charset       (name (charset-default-encoding))
        install-dir   (-> (:install-dir options ".")
                          (io/file)
                          (io/file-canonical))
        libs-dir      (io/file install-dir "libs")
        scripts-dir   (io/file install-dir "scripts")
        tmp-dir       (io/file install-dir "tmp")
        tools-dir     (io/file install-dir "tools")]
    (println)
    (println "Setting up the" (if minimal? "minimal REPL" "REPL") "on" (os-name) (os-version))
    (println "Install dir:" install-dir)
    (println "Color mode: " color-mode)
    (println "Charset:    " charset "(platform default)")
    (println)

    (try 
      (when-not (io/internet-avail?)
        (throw :internet-not-available))

      (when-not (io/exists-dir? install-dir)
        (throw :install-dir-not-exist))

      (when-not (io/exists-dir? libs-dir)
        (io/mkdirs libs-dir))

      (when-not minimal?
        (when-not (io/exists-dir? scripts-dir)
          (io/mkdirs scripts-dir))

        (when-not (io/exists-dir? (io/file scripts-dir "pdf"))
        (io/mkdirs  (io/file scripts-dir "pdf")))

        (when-not (io/exists-dir? (io/file scripts-dir "webapp"))
          (io/mkdirs (io/file scripts-dir "webapp")))

        (when-not (io/exists-dir? tools-dir)
          (io/mkdirs tools-dir)))

      (when-not (io/exists-dir? tmp-dir)
        (io/mkdirs tmp-dir))

      (let [{version :version, vendor :vendor} (java-version-info)]
        (println (str "Running on Java VM: " version " (" vendor ")"))
        (println))

      (repl-setup/copy-venice-jar libs-dir)
      (repl-setup/download-modules-libs libs-dir initial-modules silent)
      (repl-setup/setup-repl-config libs-dir)
      (repl-setup/setup-repl-launcher install-dir color-mode)
      (when-not minimal?
        (repl-setup/setup-runscript-launcher install-dir)
        (repl-setup/install-maven tools-dir)
        (repl-setup/setup-scripts scripts-dir)
        (repl-setup/setup-shebang-demo-script install-dir scripts-dir))

      (when-not (jansi-available? libs-dir)
        (throw :jansi-not-available))

      (println "Venice REPL setup completed.")
      (print-success-message install-dir)
      (println)
      :success

      ;; handle errors
      (catch :ValueException e
        (println)
        (case (ex-value e)
          :internet-not-available 
             (do (warn-internet-not-available) (ex-value e))
          :install-dir-not-exist 
             (do (warn-install-dir-not-exist install-dir) (ex-value e))
          :jansi-not-available 
             (do (warn-jansi-not-available) (ex-value e))
          (do (warn-internal-error (ex-value e)) :internal-error)))
      (catch :Exception e)
        (println)
        (warn-internal-error (ex-message e))
        :internal-error)))
