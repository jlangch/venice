;;;;   __    __         _
;;;;   \ \  / /__ _ __ (_) ___ ___
;;;;    \ \/ / _ \ '_ \| |/ __/ _ \
;;;;     \  /  __/ | | | | (_|  __/
;;;;      \/ \___|_| |_|_|\___\___|
;;;;
;;;;
;;;; Copyright 2017-2021 Venice
;;;;
;;;; Licensed under the Apache License, Version 2.0 (the "License");
;;;; you may not use this file except in compliance with the License.
;;;; You may obtain a copy of the License at
;;;;
;;;;     http://www.apache.org/licenses/LICENSE-2.0
;;;;
;;;; Unless required by applicable law or agreed to in writing, software
;;;; distributed under the License is distributed on an "AS IS" BASIS,
;;;; WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
;;;; See the License for the specific language governing permissions and
;;;; limitations under the License.

;;;; Maven utilities


(ns maven)

(load-module :ansi)


(def maven/repo "https://repo1.maven.org/maven2")


(defn
  ^{ :arglists '(
        "(maven/parse-artefact artefact)"
        "(maven/parse-artefact artefact file-suffix)"
        "(maven/parse-artefact artefact file-suffix repo)")
     :doc
        """
        Parses a Maven artefact like 'com/vaadin:vaadin-client:8.7.2'

        1. (maven/parse-artefact artefact)¶
           returns a vector with group-id, artefact-id, and version
        2. (maven/parse-artefact artefact file-suffix)¶
           returns a vector with group-id, artefact-id, version and file name
        3. (maven/parse-artefact artefact file-suffix repo)¶
           returns a vector with the Maven download URI and the file name
        """
     :examples '(
        """
        (maven/parse-artefact "org.knowm.xchart:xchart:3.6.1")
        """,
        """
        (maven/parse-artefact "org.knowm.xchart:xchart:3.6.1"
                              ".jar")
        """
        """
        (maven/parse-artefact "org.knowm.xchart:xchart:3.6.1"
                              ".jar"
                              "https://repo1.maven.org/maven2")
        """ )
     :see-also '("maven/download", "maven/get", "maven/uri") }

  maven/parse-artefact

  ([artefact]
    (let [elements (str/split artefact ":")
          group-id (first elements)
          artefact-id (second elements)
          version (third elements) ]
      [group-id artefact-id version]))

  ([artefact file-suffix]
    (let [[group-id artefact-id version] (maven/parse-artefact artefact)
          file-name (str artefact-id "-" version file-suffix)]
      [group-id artefact-id version file-name]))

  ([artefact repo file-suffix]
    (let [[group-id artefact-id version file-name] (maven/parse-artefact artefact file-suffix)
          group-path (str/replace-all group-id "." "/")
          uri (str/join "/" [repo group-path artefact-id version file-name])]
      [uri file-name])))


(defn
  ^{ :arglists '("(maven/download artefact options*)")
     :doc """
          Downloads an artefact in the format 'group-id:artefact-id:version'
          from a Maven repository. Can download any combination of the jar,
          sources, or pom artefacts to a directory.

          Options:

          | :jar {true,false}     | download the jar, defaults to true |
          | :sources {true,false} | download the sources, defaults to false |
          | :pom {true,false}     | download the pom, defaults to false |
          | :dir path             | download dir, defaults to "." |
          | :repo maven-repo      | a maven repo, defaults to "https://repo1.maven.org/maven2" |
          | :silent {true,false}  | if silent is true does not show a progress bar, defaults to true |
          """
     :examples '(
          """(maven/download "org.knowm.xchart:xchart:3.6.1")"""
          """(maven/download "org.knowm.xchart:xchart:3.6.1" :sources true :pom true)"""
          """(maven/download "org.knowm.xchart:xchart:3.6.1" :dir "." :jar false :sources true)"""
          """(maven/download "org.knowm.xchart:xchart:3.6.1" :dir "." :sources true)"""
          """(maven/download "org.knowm.xchart:xchart:3.6.1" :dir "." :sources true :repo "https://repo1.maven.org/maven2")"""
          """(maven/download "org.knowm.xchart:xchart:3.6.1" :dir "." :silent false)""" )
     :see-also '("maven/get", "maven/uri", "maven/parse-artefact") }

  maven/download [artefact & options]

  (let [opts        (apply hash-map options)
        silent      (:silent opts true)
        jar         (:jar opts true)
        sources     (:sources opts false)
        pom         (:pom opts false)
        dir         (:dir opts ".")
        repo        (:repo opts maven/repo)
        progress-fn (maven/progress-bar silent)]
    (when jar
      (let [suffix         (maven/artefact-suffix :jar)
            [uri filename] (maven/parse-artefact artefact repo suffix)]
        (maven/download-artefact-file uri dir filename progress-fn)))
    (when sources
      (let [suffix         (maven/artefact-suffix :sources)
            [uri filename] (maven/parse-artefact artefact repo suffix)]
        (maven/download-artefact-file uri dir filename progress-fn)))
    (when pom
      (let [suffix         (maven/artefact-suffix :pom)
            [uri filename] (maven/parse-artefact artefact repo suffix)]
        (maven/download-artefact-file uri dir filename progress-fn)))
    nil))


(defn
  ^{ :arglists '("(maven/get artefact type options*)")
     :doc """
          Downloads artefact in the format 'group-id:artefact-id:version'
          from a Maven repository. The artefact type 'type' is one of
          {:jar, :sources, :pom}.

          Returns the artefact as byte buffer.

          Options:

          | :repo maven-repo     | a maven repo, defaults to "https://repo1.maven.org/maven2" |
          | :silent {true,false} | if silent is true does not show a progress bar, defaults to true |
          """
     :examples '(
          """(maven/get "org.knowm.xchart:xchart:3.6.1" :jar)"""
          """(maven/get "org.knowm.xchart:xchart:3.6.1" :jar :silent false)"""
          """(maven/get "org.knowm.xchart:xchart:3.6.1" :sources)"""
          """(maven/get "org.knowm.xchart:xchart:3.6.1" :jar :repo "https://repo1.maven.org/maven2")""" )
     :see-also '("maven/download", "maven/uri", "maven/parse-artefact") }

  maven/get [artefact type & options]

  (let [opts         (apply hash-map options)
        silent       (:silent opts true)
        repo         (:repo opts maven/repo)
        suffix       (maven/artefact-suffix type)
        progress-fn  (maven/progress-bar silent)
        [uri _]      (maven/parse-artefact artefact repo suffix)]
    (maven/get-artefact-file uri progress-fn)))


(defn
  ^{ :arglists '("(maven/uri artefact type options*)")
     :doc """
          Returns an URI for an artefact in the format 'group-id:artefact-id:version'
          from a Maven repository.

          The artefact type 'type' is one of {:jar, :sources, :pom}

          Options:

          | :repo maven-repo | a maven repo, defaults to "https://repo1.maven.org/maven2" |
          """
     :examples '(
          """(maven/uri "org.knowm.xchart:xchart:3.6.1" :jar)"""
          """(maven/uri "org.knowm.xchart:xchart:3.6.1" :jar :repo "https://repo1.maven.org/maven2")""" )
     :see-also '("maven/download", "maven/get", "maven/parse-artefact") }

  maven/uri [artefact type & options]

  (let [opts         (apply hash-map options)
        repo         (:repo opts maven/repo)
        suffix       (maven/artefact-suffix type)
        [uri _]      (maven/parse-artefact artefact repo suffix)]
    uri))


(defn- maven/artefact-suffix [type]
  (case type
    :jar     ".jar"
    :sources "-sources.jar"
    :pom     ".pom"
    (throw (. :VncException :new "Invalid artefact type ~{type}"))))

(defn- maven/progress-bar [silent]
  (if silent
    nil
    (ansi/progress-bar :caption     "            "
                       :start-msg   "    started "
                       :end-msg     "            OK"
                       :failed-msg  "            FAILED")))


(defn- maven/download-artefact-file [uri target-dir target-file progress-fn]
  (let [target (str target-dir "/" target-file)]
    (when progress-fn
      (println (str "Downloading " uri))
      (println (str "         to " target "...")))
    (ansi/without-cursor
      (->> (io/download uri :binary true :progress-fn progress-fn)
           (io/spit target)))))


(defn- maven/get-artefact-file [uri progress-fn]
  (when progress-fn
    (println (str "Downloading " uri "...")))
  (ansi/without-cursor
    (io/download uri :binary true :progress-fn progress-fn)))
