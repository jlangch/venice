/*   __    __         _
 *   \ \  / /__ _ __ (_) ___ ___ 
 *    \ \/ / _ \ '_ \| |/ __/ _ \
 *     \  /  __/ | | | | (_|  __/
 *      \/ \___|_| |_|_|\___\___|
 *
 *
 * Copyright 2017-2022 Venice
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
 
// -----------------------------------------------------------------------------
// Gradle build
// -----------------------------------------------------------------------------
// 
// Venice uses Gradle 7.2
// 
// JPMS (Java Platform Module System)
//
//   The Venice Gradle build is prepared to support building a Java 8 library 
//   for JPMS. The 'compileModuleInfoJava' task provides a Java 9 compiled 
//   'module-info.class' in the root output directory.
//   See also: https://dzone.com/articles/building-java-6-8-libraries-for-jpms-in-gradle
//
//
// Build commands
//
//   - Build the Venice jar
//     $> ./gradlew shadowJar
//     $> ./gradlew --warning-mode all clean shadowJar
//
//   - Build the Venice cheatsheet (HTML & PDF)
//     $> ./gradlew clean cheatsheet
//
//   - Run the unit tests
//     $> ./gradlew test
//
//   - Micro benchmarks
//     $> ./gradlew jmh -Pinclude=".*PrecompileBenchmark"
//     $> ./gradlew -Dorg.gradle.java.home=${JAVA_11_ZULU_HOME} 
//                  jmh 
//                  -Pinclude=".*PrecompileBenchmark" 
//                  -PjvmArgs="-XX:+UseParallelGC"
//     $> ./gradlew jmh 
//                  -Pinclude=".*PrecompileBenchmark" 
//                  -PjvmArgs="-XX:+PrintCompilation -XX:CompileThreshold=1000"
//
// -----------------------------------------------------------------------------
// Release Venice:
//
//   [1] Prepare Venice
//       - Verify that all unit tests pass: ./gradlew clean test
//       - Change version in 'gradle.properties' to '1.x.y' (remove the
//         SNAPSHOT suffix)
//       - Update the ChangeLog for the release version
//       - Update files with release version: ./gradlew updateReleaseVersion
//       - Generate cheatsheets: ./gradlew cheatsheet
//       - Commit the changes with message "bumped to new version"
//       - Update the Git commit Id in the cheatsheet URLs in 'README.md':
//           ./gradlew updateReadMeCheatsheetRef
//       - Commit the 'README.md' change
//
//   [2] Publish to Maven
//       - ./gradlew -Dorg.gradle.internal.publish.checksums.insecure=true
//                   -Dorg.gradle.internal.http.socketTimeout=60000
//                   -Dorg.gradle.internal.http.connectionTimeout=60000
//                   --warning-mode all
//                   -Psigning.gnupg.keyName=[gpg-user-key-id]
//                   -PsonatypeUsername=[name] 
//                   -PsonatypePassword=[pw]
//                   clean shadowJar publish
//       - Log in to Sonatype OSSRH (https://oss.sonatype.org/)
//       - Choose Staging Repositories
//       - Find the release 'comgithubjlangch-*'
//       - Verify the contents are ok
//       - Click Close (wait for validation steps under the Activity tab 
//         to complete)
//       - Click Release
//
//   [3] Draft a GitHub Venice release
//       - Draft a new release in Git (https://github.com/jlangch/venice/releases)
//         This will create the release tag
//       - Pull the changes (commit tag)
//   
//   [4] Open new development cycle
//       - Change version in 'gradle.properties' to '1.x.y-SNAPSHOT'
//       - Add an empty ChangeLog entry for the new version
//       - Commit with message "opened new dev cycle"
//
// -----------------------------------------------------------------------------
// Gradle wrapper
//
//    Upgrade Gradle wrapper:
//      See https://docs.gradle.org/current/userguide/gradle_wrapper.html
//      Upgrade:
//         $> ./gradlew wrapper --gradle-version=7.2 --distribution-type=bin
//
//    Get initial Gradle wrapper
//      See https://guides.gradle.org/creating-new-gradle-builds/
//
//    Create and init a gradle project:
//       $> mkdir test
//       $> cd test
//       $> gradle init
//
// -----------------------------------------------------------------------------
// Gradle project configuration for Eclipse IDE
//
//    Requires the plugin 'eclipse' in the build.gradle
//
//    1.  Project checkout
//    2.  Move to your projects path (where gradle or gradlew is found)
//    3.  Unix: ./gradlew cleaneclipse
//        Win:  gradlew cleaneclipse
//    4.  Refresh your Project in Eclipse
//    5.  After that for Unix: ./gradlew eclipse     
//                       Win:  gradlew eclipse
//    6.  Refresh your Project in Eclipse
//
//    Repeat step 5) and 6) whenever the Gradle build changes
//
// -----------------------------------------------------------------------------

plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
    id 'eclipse'

    // https://github.com/johnrengelman/shadow/
    id 'com.github.johnrengelman.shadow' version '7.1.2'

/*
    // https://github.com/kordamp/jdeps-gradle-plugin
    id 'org.kordamp.gradle.jdeps' version '0.16.0'

    // https://github.com/kordamp/jdeprscan-gradle-plugin
    id 'org.kordamp.gradle.jdeprscan' version '0.10.0'
*/
}


def group = 'com.github.jlangch'

def version_regex = '[0-9]+[.][0-9]+[.][0-9]+(-SNAPSHOT)?'

def sonatypeUsername = project.properties['sonatypeUsername'] ?: "unknown"
def sonatypePassword = project.properties['sonatypePassword'] ?: "unknown"

// Detect Java 8 or 11+
def java8 = JavaVersion.current() == JavaVersion.VERSION_1_8
def java11 = JavaVersion.current() >= JavaVersion.VERSION_11
println "Gradle running on Java 8: ${java8}"
println "Gradle running on Java 11+: ${java11}"

def getCheckedOutGitCommitHash() {
    def takeFromHash = 12
    
    try {
        'git rev-parse --verify --short HEAD'.execute()
                                             .text
                                             .trim()
                                             .take(takeFromHash)
    }
    catch(Exception e) {
        println "getCheckedOutGitCommitId(): 'git' not installed, falling back to '.git' folder ..."
         
        def gitFolder = "$projectDir/.git/"
        /*
         * '.git/HEAD' contains either
         *      in case of detached head: the currently checked out commit hash
         *      otherwise: a reference to a file containing the current commit hash
         */
        def head = new File(gitFolder + "HEAD").text.split(":") // .git/HEAD
        def isCommit = head.length == 1    // e5a7c79edabbf7dd39888442df081b1c9d8e88fd
        // def isRef = head.length > 1     // ref: refs/heads/master
    
        if (isCommit) {
            return head[0].trim().take(takeFromHash) // e5a7c79edabb
        }
        else {
            def refHead = new File(gitFolder + head[1].trim()) // .git/refs/heads/master
            return refHead.text.trim().take(takeFromHash)
        }
    }
}


compileJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    options.encoding = 'UTF-8'
    options.compilerArgs = [ '-parameters' ]
    
    doLast {
        println "Java: ${JavaVersion.current()}"
    }
}

compileTestJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    options.encoding = 'UTF-8'
    options.compilerArgs += [ '-parameters' ]
}

/* JPMS support currently disabled
task compileModuleInfoJava(type: JavaCompile) {
    classpath = files() // empty
    source = 'src/main/java9/module-info.java'
    destinationDir = compileJava.destinationDir // same dir to see classes compiled by compileJava

    doFirst {
        options.compilerArgs = [
           '--release', '9',
           '--module-path', compileJava.classpath.asPath
        ]
    }
}

compileModuleInfoJava.dependsOn compileJava
classes.dependsOn compileModuleInfoJava
*/


repositories {
    mavenCentral()
}


sourceSets {
    jmh {
        java.srcDirs = ['src/jmh/java']
    }
    
    main {
        java {
            srcDirs = ['src/main/java']
        }
        resources {
            srcDirs = ['src/main/resources',
                       'doc/examples/webapp',
                       'doc/examples/scripts']
        }
    }
}


java {
    withJavadocJar()
    withSourcesJar()
}


javadoc {
    exclude "com/github/jlangch/venice/impl/**"
    exclude "com/github/jlangch/venice/examples/**"
    exclude "com/github/jlangch/venice/nanojson/**"
    source = sourceSets.main.allJava
}


test {
    // prevent unit tests that check exceptions to fail intermittently
    jvmArgs '-XX:-OmitStackTraceInFastThrow'

    useJUnitPlatform {
    }
    
    testLogging {
        showStandardStreams = false
    }
}


jar {
    exclude 'com/github/jlangch/venice/fonts/OpenSans-*.ttf'
    exclude 'com/github/jlangch/venice/fonts/SourceCodePro-*.ttf'
    
    archiveFileName = "venice-raw-${project.version}.jar"
}


shadowJar {
    // https://github.com/johnrengelman/shadow/

    relocate 'io.vavr', 'org.repackage.io.vavr'
    relocate 'org.jline', 'org.repackage.org.jline'

    def buildTimestamp = new Date().format("yyyy-MM-dd'T'HH:mm:ss")
        
    manifest {
        attributes 'Main-Class':             'com.github.jlangch.venice.Launcher',
                   'Build-Date':             buildTimestamp,
                   'Implementation-Vendor':  'com.github.jlangch',
                   'Implementation-Title':   'Venice',
                   'Implementation-Version': "${project.version}"
    }
 
    exclude 'com/github/jlangch/venice/fonts/OpenSans-*.ttf'
    exclude 'com/github/jlangch/venice/fonts/SourceCodePro-*.ttf'
   
    mergeServiceFiles()
   
    archiveFileName = "venice-${project.version}.jar"
}


publishing {
    publications {
        venicePublication(MavenPublication) {
            groupId = 'com.github.jlangch'
            artifactId = 'venice'
            
            artifact sourcesJar 
            artifact javadocJar 
              
            //artifact shadowJar
            artifact "build/libs/venice-${project.version}.jar"
            
            
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            
            pom {
                name = 'Venice'
                description = 'Venice, a sandboxed Lisp implemented in Java.'
                url = 'https://github.com/jlangch/venice/'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'jlangch'
                        name = 'Juerg Lang'
                        email = 'juerg.ch@ggaweb.ch'
                    }
                }
                scm {
                    connection = 'scm:git:git@github.com:jlangch/venice.git'
                    developerConnection = 'scm:git:git@github.com:jlangch/venice.git'
                    url = 'http://github.com:jlangch/venice/tree/master'
                    tag = 'HEAD'
                }
            }
        }
    }
    
    repositories {
        if(!version.endsWith('SNAPSHOT')) {
            maven {
                url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
                
                credentials {
                    username sonatypeUsername
                    password sonatypePassword
                }
            }
        }
    }
}


signing {
    useGpgCmd()  // use the local GPG agent to sign the artefacts
    
    sign publishing.publications.venicePublication
}

/*
jdepsReport {
    sourceSets = ['main']
    configurations = ['runtime']
}


jdeprscanReport {
    // requires Java 9+
    sourceSets = ['main']
    configurations = ['runtime']
}
*/

task bundle(type: Exec, dependsOn: [clean, shadowJar, sourcesJar, javadocJar]) {
    workingDir 'build/libs'
    
    commandLine "/bin/sh", 
                "-c", 
                "cat ../../src/main/resources/com/github/jlangch/venice/old/pom.xml " +
                  " | sed 's/__VERSION__/${project.version}/g' > ./pom.xml" + 
                  "; echo 'Bundling Venice ${project.version}'" +
                  "; echo 'Signing artifacts...'" +
                  "; gpg2 -ab pom.xml" +
                  "; gpg2 -ab venice-${project.version}-javadoc.jar" +
                  "; gpg2 -ab venice-${project.version}-sources.jar" +
                  "; gpg2 -ab venice-${project.version}.jar" +
                  "; echo 'Bundling artifacts...'" +
                  "; jar -cvf bundle.jar" +
                         " pom.xml pom.xml.asc" +
                         " venice-${project.version}-javadoc.jar venice-${project.version}-javadoc.jar.asc" +
                         " venice-${project.version}-sources.jar venice-${project.version}-sources.jar.asc" +
                         " venice-${project.version}.jar venice-${project.version}.jar.asc" +
                  "; echo 'Bundling done.'"
}

task cheatsheet(type: JavaExec, dependsOn: compileJava) {
    description="Generate cheatsheet"

    mainClass = 'com.github.jlangch.venice.CheatsheetDocGen'
    
    classpath { 
      [ sourceSets.main.runtimeClasspath,
        sourceSets.main.compileClasspath,
        "src/main/resources" ]
    }

    args "${project.version}"
}


task updateReleaseVersion {
    doLast {
        if(version.endsWith('SNAPSHOT')) {
            throw new GradleException(
            		"Rejected updating release version. ${project.version} is not a release!")
        }
 
        println "Updating version ${project.version} -> 'maven-central.svg'"
        ant.replaceregexp(
                match:   "v${version_regex}", 
                replace: "v${project.version}", 
                flags:   'g'
        ) {
            fileset(dir: 'doc', includes: 'maven-central.svg')
        }
        
        println "Updating version ${project.version} -> 'build-dependencies.md'"
        ant.replaceregexp(
                match:   "${version_regex}", 
                replace: "${project.version}", 
                flags:   'g'
        ) {
            fileset(dir: 'doc/readme', includes: 'build-dependencies.md')
        }
        
        println "Updating version ${project.version} -> 'notice.txt', 'license.txt'"
        ant.replaceregexp(
                match:   "Venice Release ${version_regex}", 
                replace: "Venice Release ${project.version}", 
                flags:   'g'
        ) {
            fileset(dir: 'src/main/resources/META-INF', includes: 'notice.txt')
            fileset(dir: 'src/main/resources/META-INF', includes: 'license.txt')
        }
          
        println "Updating version ${project.version} -> 'app.venice'"
        ant.replaceregexp(
                match:   "venice-${version_regex}[.]jar", 
                replace: "venice-${project.version}.jar", 
                flags:   'g'
        ) {
            fileset(dir: 'src/main/resources', includes: '**/app.venice')
        }
        
        println "Updating version ${project.version} -> 'readme/*.md'"
        ant.replaceregexp(
                match:   "venice-${version_regex}[.]jar", 
                replace: "venice-${project.version}.jar", 
                flags:   'g'
        ) {
            fileset(dir: 'doc/readme', includes: '*.md')
        }
        ant.replaceregexp(
                match:   "V${version_regex}", 
                replace: "V${project.version}", 
                flags:   'g'
        ) {
            fileset(dir: 'doc/readme', includes: 'start.md')
        }
        ant.replaceregexp(
                match:   "/venice/${version_regex}/", 
                replace: "/venice/${project.version}/", 
                flags:   'g'
        ) {
            fileset(dir: 'doc/readme', includes: 'start.md')
        }
        
        println "Updating version ${project.version} -> 'repl.sh', 'repl.bat'"
        ant.replaceregexp(
                match:   "venice-${version_regex}[.]jar", 
                replace: "venice-${project.version}.jar", 
                flags:   'g'
        ) {
            fileset(dir: 'src/main/resources', includes: '**/repl.sh')
            fileset(dir: 'src/main/resources', includes: '**/repl.bat')
            fileset(dir: 'doc/examples', includes: 'repl.sh')
            fileset(dir: 'doc/examples', includes: 'repl.bat')
        }
    }
}

task updateReadMeCheatsheetRef {
    doLast {
        def id = getCheckedOutGitCommitHash().take(7)
        println "Updating Cheatsheet id ${id} -> 'README.md'"
        ant.replaceregexp(
                match:   "jlangch/venice/[0-9a-f]{7}/cheatsheet", 
                replace: "jlangch/venice/${id}/cheatsheet", 
                flags:   'g'
        ) {
            fileset(dir: '.', includes: 'README.md')
        }
    }
}


task ('jmhHelp', description:'Print help for the jmh task') {
    doLast {  
        println ""
        println "Usage of jmh tasks:"
        println ""
        
        println "Only execute specific benchmark(s):"
        println "\t./gradlew jmh -Pinclude=\".*MyBenchmark.*\""
        
        println ""
        println "Specify extra profilers:"
        println "\t./gradlew jmh -Pprofilers=\"gc,stack\""
        
        println ""
        println "Prominent profilers (for full list call jmhProfilers task):"
        println "\tcomp - JitCompilations, tune your iterations"
        println "\tstack - which methods used most time"
        println "\tgc - print garbage collection stats"
        println "\ths_thr - thread usage"
        
        println ""
        println "Change report format from JSON to one of [CSV, JSON, NONE, SCSV, TEXT]:"
        println "\t./gradlew jmh -Pformat=csv"
        
        println ""
        println "Specify JVM arguments:"
        println "\t./gradlew jmh -PjvmArgs=\"-Dtest.cluster=local\""
        
        println ""
        println "Run in verification mode (execute benchmarks with minimum of fork/warmup-/benchmark-iterations):"
        println "\tgw jmh -Pverify"
        
        println ""
        println "Resources:"
        println "\thttp://tutorials.jenkov.com/java-performance/jmh.html (Introduction)"
        println "\thttp://hg.openjdk.java.net/code-tools/jmh/file/tip/jmh-samples/src/main/java/org/openjdk/jmh/samples/ (Samples)"
    }
}


task jmhProfilers(type: JavaExec, description:'Lists the available profilers for the jmh task', group: 'Development') {
    classpath = sourceSets.jmh.runtimeClasspath
    mainClass = 'org.openjdk.jmh.Main'
    args '-lprof'
}


task jmh(type: JavaExec, description: 'Executing JMH benchmarks') {
    classpath = sourceSets.jmh.runtimeClasspath
    mainClass = 'org.openjdk.jmh.Main'

    def include = project.properties.get('include', '');
    def exclude = project.properties.get('exclude');
    def format = project.properties.get('format', 'json');
    def profilers = project.properties.get('profilers');
    def jvmArgs = project.properties.get('jvmArgs')
    def verify =  project.properties.get('verify');

    def resultFile = file("build/reports/jmh/result.${format}")
    
    args include
    if (exclude) {
        args '-e', exclude
    }
    if (verify != null) { // execute benchmarks with the minimum amount of execution (only to check if they are working)
        println ">> Running in verify mode"
        args '-f' , 1
        args '-wi' , 1
        args '-i' , 1
    }
    args '-foe', 'true'   //fail-on-error
    args '-v', 'NORMAL'   //verbosity [SILENT, NORMAL, EXTRA]
    if (profilers) {
        profilers.split(',').each {
            args '-prof', it
        }
    }
    args '-jvmArgsPrepend', '-Xmx3072m'
    args '-jvmArgsPrepend', '-Xms3072m'
    if (jvmArgs) {
        for (jvmArg in jvmArgs.split(' ')) {
            args '-jvmArgsPrepend', jvmArg
        }
    }
    args '-rf', format
    args '-rff', resultFile
    
    doFirst {
        println "\nExecuting JMH with: $args \n"
        resultFile.parentFile.mkdirs()
    }
}


artifacts {
    archives sourcesJar
    archives javadocJar
}


dependencies {
    // JMH (the Java Microbenchmark Harness) has been added to the JDK 
    // starting with JDK 12; for earlier versions, the dependencies 
    // have to be added explicitly.
    jmhImplementation project
    jmhImplementation 'io.vavr:vavr:0.10.4'
    jmhImplementation 'org.openjdk.jmh:jmh-core:1.32'
    jmhAnnotationProcessor 'org.openjdk.jmh:jmh-generator-annprocess:1.32'

    // Unit tests
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
    testImplementation 'org.xhtmlrenderer:flying-saucer-core:9.1.22'
    testImplementation 'org.xhtmlrenderer:flying-saucer-pdf-openpdf:9.1.22'
    testImplementation 'com.github.librepdf:openpdf:1.3.26'
    testImplementation 'org.knowm.xchart:xchart:3.8.1'
    testImplementation 'org.apache.poi:poi:4.1.2'
    testImplementation 'org.apache.poi:poi-ooxml:4.1.2'

    // JMH (the Java Microbenchmark Harness)
    compileOnly 'org.openjdk.jmh:jmh-core:1.32'
    compileOnly 'org.openjdk.jmh:jmh-generator-annprocess:1.32'

    // Cheatsheet generation
    compileOnly 'org.xhtmlrenderer:flying-saucer-core:9.1.22'
    compileOnly 'org.xhtmlrenderer:flying-saucer-pdf-openpdf:9.1.22'
    compileOnly 'com.github.librepdf:openpdf:1.3.26'
    compileOnly 'org.knowm.xchart:xchart:3.8.1'
    
    // MS Excel support
    compileOnly 'org.apache.poi:poi:4.1.2'
    compileOnly 'org.apache.poi:poi-ooxml:4.1.2'
    compileOnly 'org.apache.poi:poi-ooxml-schemas:4.1.2'
    
    // MS Word support
    compileOnly 'org.wickedsource.docx-stamper:docx-stamper:1.4.0'
    compileOnly 'commons-io:commons-io:2.5'
    compileOnly 'org.javassist:javassist:3.21.0-GA'
    compileOnly 'org.springframework:spring-expression:4.3.6.RELEASE'   
    if (java8) {
        compileOnly 'org.docx4j:docx4j-core:8.3.4'   // Java 8
    }
    else {
        compileOnly 'org.docx4j:docx4j-core:11.4.5'  // Java 11+
        compileOnly 'javax.xml.bind:jaxb-api:2.3.1'  // Java 11+
    }
           
    // Java Microbenchmark
    compileOnly 'org.openjdk.jmh:jmh-core:1.32'
    compileOnly 'org.openjdk.jmh:jmh-generator-annprocess:1.32'

    // WebApp support
    compileOnly 'javax.servlet:javax.servlet-api:4.0.1'
      
    // runtime dependencies (repackaged)
    implementation 'io.vavr:vavr:0.10.4'
    implementation 'org.jline:jline:3.21.0'
}
